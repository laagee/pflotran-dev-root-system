module Init_module

  use PFLOTRAN_Constants_module

  implicit none

  private

#include "finclude/petscsys.h"

#include "finclude/petscvec.h"
#include "finclude/petscvec.h90"
#include "finclude/petscmat.h"
#include "finclude/petscmat.h90"
#include "finclude/petscsnes.h"
#include "finclude/petscpc.h"
#include "finclude/petscts.h"

  public :: Init
  
contains

! ************************************************************************** !

subroutine Init(simulation)
  ! 
  ! Initializes pflotran
  ! 
  ! Author: Glenn Hammond
  ! Date: 10/23/07
  ! 

  use Simulation_module
  use Option_module
  use Grid_module
  use Solver_module
  use Discretization_module
  use Realization_class
  use Material_module
  use Timestepper_module
  use Field_module
  use Connection_module   
  use Coupler_module
  use Debug_module
  use Convergence_module
  use Waypoint_module
  use Patch_module
! use Mass_Balance_module
  use Logging_module  
  use Database_module
  use Database_hpt_module
  use Input_Aux_module
  use Condition_Control_module
  use Subsurface_module
  
  use Flash2_module
  use Mphase_module
  use Immis_module
  use Miscible_module
  use Richards_module
  use Richards_MFD_module
  use TH_module
  use General_module
  
  use Reactive_Transport_module
  use Reaction_Aux_module, only : ACT_COEF_FREQUENCY_OFF
  
  use Secondary_Continuum_module, only : SecondaryRTUpdateIterate
  
  use Global_module
  use Variables_module
  
  use EOS_module
  use EOS_Water_module
!  use Utility_module
  use Output_module
  use Output_Aux_module
  use Regression_module
    
  use Surface_Field_module
  use Surface_Flow_module
  use Surface_Global_module
  use Surface_Init_module, only : SurfaceInitReadRequiredCards, &
                                  SurfaceInitMatPropToRegions, &
                                  SurfaceInitReadRegionFiles
  use Surface_Realization_class
  use Surface_TH_module
  use Unstructured_Grid_module

  use Geomechanics_Realization_class
  use Geomechanics_Init_module, only : GeomechicsInitReadRequiredCards, &
                                       GeomechInitMatPropToGeomechRegions
  use Geomechanics_Grid_module
  use Geomechanics_Discretization_module
  use Geomechanics_Field_module
  use Geomechanics_Global_module
  use Geomechanics_Force_module

  implicit none
  
  type(simulation_type) :: simulation
  character(len=MAXSTRINGLENGTH) :: filename, filename_out

  type(timestepper_type), pointer :: flow_timestepper
  type(timestepper_type), pointer :: tran_timestepper
  type(solver_type), pointer :: flow_solver
  type(solver_type), pointer :: tran_solver
  type(realization_type), pointer :: realization
  type(discretization_type), pointer :: discretization
  type(grid_type), pointer :: grid
  type(option_type), pointer :: option
  type(field_type), pointer :: field
  type(patch_type), pointer :: patch  
  type(debug_type), pointer :: debug
  type(waypoint_list_type), pointer :: waypoint_list
  type(input_type), pointer :: input
  type(output_variable_type), pointer :: output_variable
  character(len=MAXSTRINGLENGTH) :: string
  Vec :: global_vec
  PetscInt :: temp_int
  PetscInt :: flowortranpc    
  PetscErrorCode :: ierr
  PCSide:: pcside
  PetscReal :: dum1
  PetscReal :: min_value
  SNESLineSearch :: linesearch
  type(timestepper_type), pointer               :: surf_flow_timestepper
  type(solver_type), pointer                :: surf_flow_solver
  type(surface_field_type), pointer         :: surf_field
  type(surface_realization_type), pointer   :: surf_realization
  type(solver_type), pointer                :: geomech_solver
  type(timestepper_type), pointer               :: geomech_timestepper
  type(geomech_field_type), pointer         :: geomech_field
  type(geomech_realization_type), pointer   :: geomech_realization

  ! popped in TimestepperInitializeRun()
  call PetscLogStagePush(logging%stage(INIT_STAGE),ierr);CHKERRQ(ierr)
  call PetscLogEventBegin(logging%event_init,ierr);CHKERRQ(ierr)
  
  ! set pointers to objects
  flow_timestepper => simulation%flow_timestepper
  tran_timestepper => simulation%tran_timestepper
  realization => simulation%realization
  discretization => realization%discretization
  option => realization%option
  field => realization%field
  debug => realization%debug
  input => realization%input
  surf_realization  => simulation%surf_realization
  surf_flow_timestepper => simulation%surf_flow_timestepper
  surf_field        => surf_realization%surf_field  
  geomech_realization => simulation%geomech_realization
  geomech_timestepper => simulation%geomech_timestepper
  geomech_field => geomech_realization%geomech_field
  
  nullify(flow_solver)
  nullify(tran_solver)

  ! sets pointers to EOS procedures
  call EOSInit()
  
  if (OptionPrintToScreen(option)) then
    temp_int = 6
    call InitPrintPFLOTRANHeader(option,temp_int)
  endif
  
  realization%input => InputCreate(IN_UNIT,option%input_filename,option)

  filename_out = trim(option%global_prefix) // trim(option%group_prefix) // &
                 '.out'

  if (option%myrank == option%io_rank .and. option%print_to_file) then
    open(option%fid_out, file=filename_out, action="write", status="unknown")
  endif

  if (OptionPrintToFile(option)) then
    call InitPrintPFLOTRANHeader(option,option%fid_out)
  endif
  
  ! read required cards
  call InitReadRequiredCardsFromInput(realization)
  !geh: surf_realization%input is never freed
  surf_realization%input => InputCreate(IN_UNIT,option%input_filename,option)
  surf_realization%subsurf_filename = realization%discretization%filename
  call SurfaceInitReadRequiredCards(simulation%surf_realization)

  geomech_realization%input => InputCreate(IN_UNIT,option%input_filename,option)
  call GeomechicsInitReadRequiredCards(simulation%geomech_realization)

  patch => realization%patch

  if (associated(patch)) then
     if (associated(patch%grid)) then
        grid => patch%grid
     endif
  endif

  ! process command line options
  call OptionCheckCommandLine(option)

  waypoint_list => WaypointListCreate()
  realization%waypoint_list => waypoint_list
  
  ! initialize flow mode
  if (len_trim(option%flowmode) > 0) then
    ! set the operational mode (e.g.  MPH_MODE, etc)
    call setFlowMode(option)
    flow_solver => flow_timestepper%solver
  else
    option%nphase = 1
    option%liquid_phase = 1
    option%use_isothermal = PETSC_TRUE  ! assume default isothermal when only transport
    call TimestepperDestroy(simulation%flow_timestepper)
    nullify(flow_timestepper)
  endif
    
  ! initialize transport mode
  if (option%ntrandof > 0) then
    tran_solver => tran_timestepper%solver
  else
    call TimestepperDestroy(simulation%tran_timestepper)
    nullify(tran_timestepper)
  endif

  ! initialize surface-flow mode
  if (option%surf_flow_on) then
    call setSurfaceFlowMode(option)
    surf_flow_solver => surf_flow_timestepper%solver
    waypoint_list => WaypointListCreate()
    surf_realization%waypoint_list => waypoint_list
  else
    call TimestepperDestroy(simulation%surf_flow_timestepper)
    nullify(surf_flow_solver)
  endif

  ! initialize surface-flow mode
  if (option%ngeomechdof > 0) then
    geomech_solver => geomech_timestepper%solver
    waypoint_list => WaypointListCreate()
    geomech_realization%waypoint_list => waypoint_list
  else
    call TimestepperDestroy(simulation%geomech_timestepper)
    nullify(geomech_solver)
  endif

  ! initialize plot variables
  realization%output_option%output_variable_list => OutputVariableListCreate()
  realization%output_option%aveg_output_variable_list => OutputVariableListCreate()
  ! initialize plot variables
  simulation%surf_realization%output_option%output_variable_list => &
    OutputVariableListCreate()
  simulation%surf_realization%output_option%aveg_output_variable_list => &
    OutputVariableListCreate()
  geomech_realization%output_option%output_variable_list => &
    OutputVariableListCreate()
  geomech_realization%output_option%aveg_output_variable_list => &
    OutputVariableListCreate()

  ! read in the remainder of the input file
  call InitReadInput(simulation)
  call InputDestroy(realization%input)

  ! destroy other 'input' used above
  ! (TODO: fmy- need to check if this is the right place to do so)
  call InputDestroy(surf_realization%input)    !allocated in Line 182)
  call InputDestroy(geomech_realization%input) ! allocated in Line 186)

  ! initialize reference density
  if (option%reference_water_density < 1.d-40) then
#ifndef DONT_USE_WATEOS
    call EOSWaterDensity(option%reference_temperature, &
                         option%reference_pressure, &
                         option%reference_water_density, &
                         dum1,ierr)    
#else
    call EOSWaterdensity(option%reference_temperature,option%reference_pressure, &
                 option%reference_water_density,dum1,ierr)
#endif                 
  endif
  
  ! read reaction database
  
  if (associated(realization%reaction)) then
    if (realization%reaction%use_full_geochemistry) then
        call DatabaseRead(realization%reaction,option)
        call BasisInit(realization%reaction,option)    
    else
      ! turn off activity coefficients since the database has not been read
      realization%reaction%act_coef_update_frequency = ACT_COEF_FREQUENCY_OFF
      allocate(realization%reaction%primary_species_print(option%ntrandof))
      realization%reaction%primary_species_print = PETSC_TRUE
    endif
  endif

  ! Initialize flow databases (e.g. span wagner, etc.)
  select case(option%iflowmode)
    case(MPH_MODE, FLASH2_MODE, IMS_MODE)
      call init_span_wanger(realization)
  end select
  
  ! SK 09/30/13, Added to check if Mphase is called with OS
  if (option%transport%reactive_transport_coupling == OPERATOR_SPLIT .and. &
      option%iflowmode == MPH_MODE) then
    option%io_buffer = 'Operator split not implemented with MPHASE. ' // &
                       'Switching to Global Implicit.'
    call printWrnMsg(option)
    option%transport%reactive_transport_coupling = GLOBAL_IMPLICIT
  endif
  
  ! create grid and allocate vectors
  call RealizationCreateDiscretization(realization)
  
  if (option%surf_flow_on) then
    call SurfRealizCreateDiscretization(simulation%surf_realization)
  endif

  if (option%ngeomechdof > 0) then
    call GeomechRealizCreateDiscretization(geomech_realization)
  endif

  call RegressionCreateMapping(simulation%regression,realization)

  if (realization%discretization%itype == STRUCTURED_GRID .or. &
      realization%discretization%itype == STRUCTURED_GRID_MIMETIC) then
    if (OptionPrintToScreen(option)) then
      write(*,'(/," Requested processors and decomposition = ", &
               & i5,", npx,y,z= ",3i4)') &
          option%mycommsize,grid%structured_grid%npx, &
          grid%structured_grid%npy,grid%structured_grid%npz
      write(*,'(" Actual decomposition: npx,y,z= ",3i4,/)') &
          grid%structured_grid%npx_final,grid%structured_grid%npy_final, &
          grid%structured_grid%npz_final
    endif
    if (OptionPrintToScreen(option)) then
      write(option%fid_out,'(/," Requested processors and decomposition = ", &
                           & i5,", npx,y,z= ",3i4)') &
          option%mycommsize,grid%structured_grid%npx,grid%structured_grid%npy, &
          grid%structured_grid%npz
      write(option%fid_out,'(" Actual decomposition: npx,y,z= ",3i4,/)') &
          grid%structured_grid%npx_final,grid%structured_grid%npy_final, &
          grid%structured_grid%npz_final
    endif
  endif

  ! update flow mode based on optional input
  if (option%nflowdof > 0) then
  
    if (flow_solver%J_mat_type == MATAIJ) then
      select case(option%iflowmode)
        case(MPH_MODE,TH_MODE,IMS_MODE, FLASH2_MODE, G_MODE, MIS_MODE)
          option%io_buffer = 'AIJ matrix not supported for current mode: '// &
                             option%flowmode
          call printErrMsg(option)
      end select
    endif

    if (OptionPrintToScreen(option)) then
      write(*,'(" number of dofs = ",i3,", number of phases = ",i3,i2)') &
        option%nflowdof,option%nphase
      select case(option%iflowmode)
        case(FLASH2_MODE)
          write(*,'(" mode = FLASH2: p, T, s/X")')
        case(MPH_MODE)
          write(*,'(" mode = MPH: p, T, s/X")')
        case(IMS_MODE)
          write(*,'(" mode = IMS: p, T, s")')
        case(MIS_MODE)
          write(*,'(" mode = MIS: p, Xs")')
        case(TH_MODE)
          write(*,'(" mode = TH: p, T")')
        case(RICHARDS_MODE)
          write(*,'(" mode = Richards: p")')  
        case(G_MODE)    
      end select
    endif

    call printMsg(option,"  Beginning setup of FLOW SNES ")

    call SolverCreateSNES(flow_solver,option%mycomm)  
    call SNESSetOptionsPrefix(flow_solver%snes, "flow_",ierr);CHKERRQ(ierr)
    call SolverCheckCommandLine(flow_solver)

    if (flow_solver%Jpre_mat_type == '') then
      if (flow_solver%J_mat_type /= MATMFFD) then
        flow_solver%Jpre_mat_type = flow_solver%J_mat_type
      else
        flow_solver%Jpre_mat_type = MATBAIJ
      endif
    endif

    call DiscretizationCreateJacobian(discretization,NFLOWDOF, &
                                      flow_solver%Jpre_mat_type, &
                                      flow_solver%Jpre, &
                                      option)

    call MatSetOptionsPrefix(flow_solver%Jpre,"flow_",ierr);CHKERRQ(ierr)

    if (flow_solver%J_mat_type /= MATMFFD) then
      flow_solver%J = flow_solver%Jpre
    endif

    if (flow_solver%use_galerkin_mg) then
      call DiscretizationCreateInterpolation(discretization,NFLOWDOF, &
                                             flow_solver%interpolation, &
                                             flow_solver%galerkin_mg_levels_x, &
                                             flow_solver%galerkin_mg_levels_y, &
                                             flow_solver%galerkin_mg_levels_z, &
                                             option)
    endif
    
    select case(option%iflowmode)
      case(TH_MODE)
        call SNESSetFunction(flow_solver%snes,field%flow_r,THResidual, &
                             realization,ierr);CHKERRQ(ierr)
      case(RICHARDS_MODE)
        select case(realization%discretization%itype)
          case(STRUCTURED_GRID_MIMETIC,UNSTRUCTURED_GRID_MIMETIC)
            call SNESSetFunction(flow_solver%snes,field%flow_r_faces, &
                                 RichardsResidualMFDLP, &
                                 realization,ierr);CHKERRQ(ierr)
          case default
            call SNESSetFunction(flow_solver%snes,field%flow_r, &
                                 RichardsResidual, &
                                 realization,ierr);CHKERRQ(ierr)
        end select
      case(MPH_MODE)
        call SNESSetFunction(flow_solver%snes,field%flow_r,MphaseResidual, &
                             realization,ierr);CHKERRQ(ierr)
      case(IMS_MODE)
        call SNESSetFunction(flow_solver%snes,field%flow_r,ImmisResidual, &
                             realization,ierr);CHKERRQ(ierr)
      case(MIS_MODE)
        call SNESSetFunction(flow_solver%snes,field%flow_r,MiscibleResidual, &
                             realization,ierr);CHKERRQ(ierr)
      case(FLASH2_MODE)
        call SNESSetFunction(flow_solver%snes,field%flow_r,FLASH2Residual, &
                             realization,ierr);CHKERRQ(ierr)
      case(G_MODE)
        call SNESSetFunction(flow_solver%snes,field%flow_r,GeneralResidual, &
                             realization,ierr);CHKERRQ(ierr)
    end select
    
    if (flow_solver%J_mat_type == MATMFFD) then
      call MatCreateSNESMF(flow_solver%snes,flow_solver%J,ierr);CHKERRQ(ierr)
    endif

    select case(option%iflowmode)
      case(TH_MODE)
        call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             THJacobian,realization,ierr);CHKERRQ(ierr)
      case(RICHARDS_MODE)
        select case(realization%discretization%itype)
          case(STRUCTURED_GRID_MIMETIC,UNSTRUCTURED_GRID_MIMETIC)
            call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             RichardsJacobianMFDLP,realization, &
                                 ierr);CHKERRQ(ierr)
          case default !sp 
            call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             RichardsJacobian,realization,ierr);CHKERRQ(ierr)
        end select

      case(MPH_MODE)
        call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             MPHASEJacobian,realization,ierr);CHKERRQ(ierr)
      case(IMS_MODE)
        call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             ImmisJacobian,realization,ierr);CHKERRQ(ierr)
      case(MIS_MODE)
        call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             MiscibleJacobian,realization,ierr);CHKERRQ(ierr)
      case(FLASH2_MODE)
        call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             FLASH2Jacobian,realization,ierr);CHKERRQ(ierr)
      case(G_MODE)
        call SNESSetJacobian(flow_solver%snes,flow_solver%J,flow_solver%Jpre, &
                             GeneralJacobian,realization,ierr);CHKERRQ(ierr)
    end select
    
    ! by default turn off line search
    call SNESGetLineSearch(flow_solver%snes, linesearch, ierr);CHKERRQ(ierr)
    call SNESLineSearchSetType(linesearch, SNESLINESEARCHBASIC,  &
                               ierr);CHKERRQ(ierr)
    ! Have PETSc do a SNES_View() at the end of each solve if verbosity > 0.
    if (option%verbosity >= 2) then
      string = '-flow_snes_view'
      call PetscOptionsInsertString(string, ierr);CHKERRQ(ierr)
    endif

    call SolverSetSNESOptions(flow_solver)

    ! If we are using a structured grid, set the corresponding flow DA 
    ! as the DA for the PCEXOTIC preconditioner, in case we choose to use it.
    ! The PCSetDA() call is ignored if the PCEXOTIC preconditioner is 
    ! no used.  We need to put this call after SolverCreateSNES() so that 
    ! KSPSetFromOptions() will already have been called.
    ! I also note that this preconditioner is intended only for the flow 
    ! solver.  --RTM
    if ((realization%discretization%itype == STRUCTURED_GRID_MIMETIC).or.&
                (realization%discretization%itype == STRUCTURED_GRID)) then
      call PCSetDM(flow_solver%pc, &
                   realization%discretization%dm_nflowdof,ierr);CHKERRQ(ierr)
    endif

    option%io_buffer = 'Solver: ' // trim(flow_solver%ksp_type)
    call printMsg(option)
    option%io_buffer = 'Preconditioner: ' // trim(flow_solver%pc_type)
    call printMsg(option)

    ! shell for custom convergence test.  The default SNES convergence test  
    ! is call within this function. 
    flow_timestepper%convergence_context => &
      ConvergenceContextCreate(flow_solver,option,grid)
    call SNESSetConvergenceTest(flow_solver%snes,ConvergenceTest, &
                                flow_timestepper%convergence_context, &
                                PETSC_NULL_FUNCTION,ierr);CHKERRQ(ierr)

    
 
    call SNESGetLineSearch(flow_solver%snes, linesearch, ierr);CHKERRQ(ierr)
    select case(option%iflowmode)
      case(RICHARDS_MODE)
        if (dabs(option%pressure_dampening_factor) > 0.d0 .or. &
            dabs(option%saturation_change_limit) > 0.d0) then
          call SNESLineSearchSetPreCheck(linesearch, &
                                         RichardsCheckUpdatePre, &
                                         realization,ierr);CHKERRQ(ierr)
        endif
      case(G_MODE)
        call SNESLineSearchSetPreCheck(linesearch, &
                                       GeneralCheckUpdatePre, &
                                       realization,ierr);CHKERRQ(ierr)
      case(TH_MODE)
        if (dabs(option%pressure_dampening_factor) > 0.d0 .or. &
            dabs(option%pressure_change_limit) > 0.d0 .or. &
            dabs(option%temperature_change_limit) > 0.d0) then
          call SNESLineSearchSetPreCheck(linesearch, &
                                         THCheckUpdatePre, &
                                         realization,ierr);CHKERRQ(ierr)
        endif
    end select
    
    
    if (flow_solver%check_post_convergence) then
      call SNESGetLineSearch(flow_solver%snes, linesearch, ierr);CHKERRQ(ierr)
      select case(option%iflowmode)
        case(RICHARDS_MODE)
          call SNESLineSearchSetPostCheck(linesearch, &
                                          RichardsCheckUpdatePost, &
                                          realization,ierr);CHKERRQ(ierr)
        case(G_MODE)
          call SNESLineSearchSetPostCheck(linesearch, &
                                          GeneralCheckUpdatePost, &
                                          realization,ierr);CHKERRQ(ierr)
        case(TH_MODE)
          call SNESLineSearchSetPostCheck(linesearch, &
                                          THCheckUpdatePost, &
                                          realization,ierr);CHKERRQ(ierr)
      end select
    endif
        
    
    call printMsg(option,"  Finished setting up FLOW SNES ")

    if (option%surf_flow_on) then

      ! Setup PETSc TS for explicit surface flow solution
      call printMsg(option,"  Beginning setup of SURF FLOW TS ")

      call SolverCreateTS(surf_flow_solver,option%mycomm)
      call TSSetProblemType(surf_flow_solver%ts,TS_NONLINEAR, &
                            ierr);CHKERRQ(ierr)
      select case(option%iflowmode)
        case (RICHARDS_MODE)
          call TSSetRHSFunction(surf_flow_solver%ts,PETSC_NULL_OBJECT, &
                                SurfaceFlowRHSFunction, &
                                simulation%surf_realization, &
                                ierr);CHKERRQ(ierr)
        case (TH_MODE)
          call TSSetRHSFunction(surf_flow_solver%ts,PETSC_NULL_OBJECT, &
                                SurfaceTHRHSFunction, &
                                simulation%surf_realization, &
                                ierr);CHKERRQ(ierr)
          call TSSetIFunction(surf_flow_solver%ts,PETSC_NULL_OBJECT, &
                              SurfaceTHIFunction, &
                              simulation%surf_realization, &
                              ierr);CHKERRQ(ierr)
      end select
      call TSSetDuration(surf_flow_solver%ts,ONE_INTEGER, &
                         simulation%surf_realization%waypoint_list%last%time, &
                         ierr);CHKERRQ(ierr)

    endif ! if (option%surf_flow_on)

  endif

  ! update geomechanics mode based on optional input
  if (option%ngeomechdof > 0) then

    if (geomech_solver%J_mat_type == MATAIJ) then
      option%io_buffer = 'AIJ matrix not supported for geomechanics.'
      call printErrMsg(option)
    endif

    call printMsg(option,"  Beginning setup of GEOMECH SNES ")
    
    call SolverCreateSNES(geomech_solver,option%mycomm)  
    call SNESSetOptionsPrefix(geomech_solver%snes, "geomech_", &
                              ierr);CHKERRQ(ierr)
    call SolverCheckCommandLine(geomech_solver)
        
    if (geomech_solver%Jpre_mat_type == '') then
      geomech_solver%Jpre_mat_type = geomech_solver%J_mat_type
    endif
    call GeomechDiscretizationCreateJacobian(geomech_realization% &
                                             geomech_discretization,NGEODOF, &
                                             geomech_solver%Jpre_mat_type, &
                                             geomech_solver%Jpre,option)

    geomech_solver%J = geomech_solver%Jpre
    call MatSetOptionsPrefix(geomech_solver%Jpre,"geomech_", &
                             ierr);CHKERRQ(ierr)
    

    call SNESSetFunction(geomech_solver%snes,geomech_field%disp_r, &
                         GeomechForceResidual, &
                         simulation%geomech_realization,ierr);CHKERRQ(ierr)

    call SNESSetJacobian(geomech_solver%snes,geomech_solver%J, &
                         geomech_solver%Jpre,GeomechForceJacobian, &
                         simulation%geomech_realization,ierr);CHKERRQ(ierr)
    ! by default turn off line search
    call SNESGetLineSearch(geomech_solver%snes,linesearch, ierr);CHKERRQ(ierr)
    call SNESLineSearchSetType(linesearch,SNESLINESEARCHBASIC, &
                               ierr);CHKERRQ(ierr)

    ! Have PETSc do a SNES_View() at the end of each solve if verbosity > 0.
    if (option%verbosity >= 1) then
      string = '-geomech_snes_view'
      call PetscOptionsInsertString(string, ierr);CHKERRQ(ierr)
    endif

    call SolverSetSNESOptions(geomech_solver)

    option%io_buffer = 'Solver: ' // trim(geomech_solver%ksp_type)
    call printMsg(option)
    option%io_buffer = 'Preconditioner: ' // trim(geomech_solver%pc_type)
    call printMsg(option)

    ! shell for custom convergence test.  The default SNES convergence test
    ! is call within this function.
    geomech_timestepper%convergence_context => &
    ConvergenceContextCreate(geomech_solver,option,grid) ! Need to change this for geomech
    call SNESSetConvergenceTest(geomech_solver%snes,ConvergenceTest, &
                                geomech_timestepper%convergence_context, &
                                PETSC_NULL_FUNCTION,ierr);CHKERRQ(ierr)

    call printMsg(option,"  Finished setting up GEOMECH SNES ")
  
  endif

  ! update transport mode based on optional input
  if (option%ntrandof > 0) then

    call printMsg(option,"  Beginning setup of TRAN SNES ")
    
    call SolverCreateSNES(tran_solver,option%mycomm)  
    call SNESSetOptionsPrefix(tran_solver%snes, "tran_",ierr);CHKERRQ(ierr)
    call SolverCheckCommandLine(tran_solver)
    
    if (option%transport%reactive_transport_coupling == GLOBAL_IMPLICIT) then
      if (tran_solver%Jpre_mat_type == '') then
        if (tran_solver%J_mat_type /= MATMFFD) then
          tran_solver%Jpre_mat_type = tran_solver%J_mat_type
        else
          tran_solver%Jpre_mat_type = MATBAIJ
        endif
      endif
      call DiscretizationCreateJacobian(discretization,NTRANDOF, &
                                        tran_solver%Jpre_mat_type, &
                                        tran_solver%Jpre,option)
    else
      tran_solver%J_mat_type = MATAIJ
      tran_solver%Jpre_mat_type = MATAIJ

      call DiscretizationCreateJacobian(discretization,ONEDOF, &
                                        tran_solver%Jpre_mat_type, &
                                        tran_solver%Jpre,option)
    endif

    if (tran_solver%J_mat_type /= MATMFFD) then
      tran_solver%J = tran_solver%Jpre
    endif
    
    call MatSetOptionsPrefix(tran_solver%Jpre,"tran_",ierr);CHKERRQ(ierr)
    
    if (tran_solver%use_galerkin_mg) then
      call DiscretizationCreateInterpolation(discretization,NTRANDOF, &
                                             tran_solver%interpolation, &
                                             tran_solver%galerkin_mg_levels_x, &
                                             tran_solver%galerkin_mg_levels_y, &
                                             tran_solver%galerkin_mg_levels_z, &
                                             option)
    endif

    if (option%transport%reactive_transport_coupling == GLOBAL_IMPLICIT) then

      call SNESSetFunction(tran_solver%snes,field%tran_r,RTResidual,&
                           realization,ierr);CHKERRQ(ierr)

      if (tran_solver%J_mat_type == MATMFFD) then
        call MatCreateSNESMF(tran_solver%snes,tran_solver%J, &
                             ierr);CHKERRQ(ierr)
      endif
      
      call SNESSetJacobian(tran_solver%snes,tran_solver%J,tran_solver%Jpre, &
                           RTJacobian,realization,ierr);CHKERRQ(ierr)

      ! this could be changed in the future if there is a way to ensure that the linesearch
      ! update does not perturb concentrations negative.
      call SNESGetLineSearch(tran_solver%snes, linesearch, ierr);CHKERRQ(ierr)
      call SNESLineSearchSetType(linesearch, SNESLINESEARCHBASIC,  &
                                 ierr);CHKERRQ(ierr)
      
      if (option%use_mc) then
        call SNESLineSearchSetPostCheck(linesearch, &
                                        SecondaryRTUpdateIterate, &
                                        realization,ierr);CHKERRQ(ierr)
      endif
      
      ! Have PETSc do a SNES_View() at the end of each solve if verbosity > 0.
      if (option%verbosity >= 2) then
        string = '-tran_snes_view'
        call PetscOptionsInsertString(string, ierr);CHKERRQ(ierr)
      endif

    endif

    ! ensure setting of SNES options since they set KSP and PC options too
    call SolverSetSNESOptions(tran_solver)

    option%io_buffer = 'Solver: ' // trim(tran_solver%ksp_type)
    call printMsg(option)
    option%io_buffer = 'Preconditioner: ' // trim(tran_solver%pc_type)
    call printMsg(option)

    if (option%transport%reactive_transport_coupling == GLOBAL_IMPLICIT) then

      ! shell for custom convergence test.  The default SNES convergence test  
      ! is call within this function. 
      tran_timestepper%convergence_context => &
        ConvergenceContextCreate(tran_solver,option,grid)
      call SNESSetConvergenceTest(tran_solver%snes,ConvergenceTest, &
                                  tran_timestepper%convergence_context, &
                                  PETSC_NULL_FUNCTION,ierr);CHKERRQ(ierr)

      ! this update check must be in place, otherwise reactive transport is likely
      ! to fail
      if (associated(realization%reaction)) then
        call SNESGetLineSearch(tran_solver%snes, linesearch,  &
                               ierr);CHKERRQ(ierr)
        if (realization%reaction%check_update) then
          call SNESLineSearchSetPreCheck(linesearch,RTCheckUpdatePre, &
                                         realization,ierr);CHKERRQ(ierr)
        endif
        if (tran_solver%check_post_convergence) then
          call SNESLineSearchSetPostCheck(linesearch,RTCheckUpdatePost, &
                                          realization,ierr);CHKERRQ(ierr)
        endif
      endif
    endif
    
    call printMsg(option,"  Finished setting up TRAN SNES ")
  
  endif

  if (OptionPrintToScreen(option)) write(*,'("++++++++++++++++++++++++++++++++&
                     &++++++++++++++++++++++++++++",/)')

  call PetscLogEventBegin(logging%event_setup,ierr);CHKERRQ(ierr)
  ! read any regions provided in external files
  call readRegionFiles(realization)
  ! clip regions and set up boundary connectivity, distance  
  call RealizationLocalizeRegions(realization)
  call RealizationPassPtrsToPatches(realization)
  ! link conditions with regions through couplers and generate connectivity
  call RealProcessMatPropAndSatFunc(realization)
  ! must process conditions before couplers in order to determine dataset types
  call RealizationProcessConditions(realization)
  call RealizationProcessCouplers(realization)
  call SandboxesSetup(realization)
  call RealProcessFluidProperties(realization)
  call SubsurfInitMaterialProperties(realization)
  ! assignVolumesToMaterialAuxVars() must be called after 
  ! RealizInitMaterialProperties() where the Material object is created 
  call assignVolumesToMaterialAuxVars(realization)
  if (option%root_system) call assignRootPropToRegions(realization)
  if(realization%discretization%lsm_flux_method) &
    call GridComputeMinv(realization%discretization%grid, &
                         realization%discretization%stencil_width,option)

  call RealizationInitAllCouplerAuxVars(realization)
  if (option%ntrandof > 0) then
    call printMsg(option,"  Setting up TRAN Realization ")
    call RealizationInitConstraints(realization)
    call printMsg(option,"  Finished setting up TRAN Realization ")  
  endif
  call RealizationPrintCouplers(realization)
  call PetscLogEventEnd(logging%event_setup,ierr);CHKERRQ(ierr)
  if (.not.option%steady_state) then
    ! add waypoints associated with boundary conditions, source/sinks etc. to list
    call RealizationAddWaypointsToList(realization)
    ! fill in holes in waypoint data
    call WaypointListFillIn(option,realization%waypoint_list)
    call WaypointListRemoveExtraWaypnts(option,realization%waypoint_list)
  ! geh- no longer needed
  !  ! convert times from input time to seconds
  !  call WaypointConvertTimes(realization%waypoint_list,realization%output_option%tconv)
  endif
  
  if (associated(flow_timestepper)) then
    flow_timestepper%cur_waypoint => realization%waypoint_list%first
  endif
  if (associated(tran_timestepper)) then
    tran_timestepper%cur_waypoint => realization%waypoint_list%first
  endif
  
  ! initialize global auxiliary variable object
  call GlobalSetup(realization)
  ! initialize FLOW
  ! set up auxillary variable arrays
  if (option%nflowdof > 0) then
    select case(option%iflowmode)
      case(TH_MODE)
        call THSetup(realization)
      case(RICHARDS_MODE)
        call RichardsSetup(realization)
      case(MPH_MODE)
        call MphaseSetup(realization)
      case(IMS_MODE)
        call ImmisSetup(realization)
      case(MIS_MODE)
        call MiscibleSetup(realization)
      case(FLASH2_MODE)
        call Flash2Setup(realization)
      case(G_MODE)
        call MaterialSetup(realization%patch%aux%Material%material_parameter, &
                           patch%material_property_array, &
                           patch%characteristic_curves_array, &
                           realization%option)
        call GeneralSetup(realization)
    end select
  
    ! assign initial conditionsRealizAssignFlowInitCond
    call CondControlAssignFlowInitCond(realization)

    ! override initial conditions if they are to be read from a file
    if (len_trim(option%initialize_flow_filename) > 1) then
      call readFlowInitialCondition(realization, &
                                    option%initialize_flow_filename)
    endif
  
    select case(option%iflowmode)
      case(TH_MODE)
        call THUpdateAuxVars(realization)
      case(RICHARDS_MODE)
#ifdef DASVYAT
       if (option%mimetic) then
!        call RichardsInitialPressureReconstruction(realization)
!        write(*,*) "RichardsInitialPressureReconstruction"
!        read(*,*)
       end if
#endif 
        call RichardsUpdateAuxVars(realization)
      case(MPH_MODE)
        call MphaseUpdateAuxVars(realization)
      case(IMS_MODE)
        call ImmisUpdateAuxVars(realization)
      case(MIS_MODE)
        call MiscibleUpdateAuxVars(realization)
      case(FLASH2_MODE)
        call Flash2UpdateAuxVars(realization)
      case(G_MODE)
        call GeneralUpdateAuxVars(realization,PETSC_TRUE)
    end select
  else ! no flow mode specified
    if (len_trim(realization%nonuniform_velocity_filename) > 0) then
      call InitReadVelocityField(realization)
    endif
  endif

  if (option%ntrandof > 0) then

    call RTSetup(realization)

    ! initialize densities and saturations
    if (option%nflowdof == 0) then
      call GlobalSetAuxVarScalar(realization,option%reference_pressure, &
                                 LIQUID_PRESSURE)
      call GlobalSetAuxVarScalar(realization,option%reference_temperature, &
                                 TEMPERATURE)
      call GlobalSetAuxVarScalar(realization,option%reference_saturation, &
                                 LIQUID_SATURATION)
      call GlobalSetAuxVarScalar(realization,option%reference_water_density, &
                                 LIQUID_DENSITY)
    else
      call GlobalUpdateAuxVars(realization,TIME_T,0.d0)
      call GlobalWeightAuxVars(realization,0.d0)
    endif

    ! initial concentrations must be assigned after densities are set !!!
    call CondControlAssignTranInitCond(realization)
  endif
  
  ! Add plot variables that are not mode specific
  if (realization%output_option%print_porosity) then
    ! add porosity to header
    call OutputVariableAddToList( &
           realization%output_option%output_variable_list, &
           'Porosity',OUTPUT_GENERIC,'-',POROSITY)  
  endif
  if (realization%output_option%print_permeability) then
    ! add permeability to header
    if (MaterialAnisotropyExists(realization%material_properties)) then
      call OutputVariableAddToList( &
             realization%output_option%output_variable_list, &
             'Permeability X',OUTPUT_GENERIC,'m^2',PERMEABILITY)
      call OutputVariableAddToList( &
             realization%output_option%output_variable_list, &
             'Permeability Y',OUTPUT_GENERIC,'m^2',PERMEABILITY_Y)
      call OutputVariableAddToList( &
             realization%output_option%output_variable_list, &
             'Permeability Z',OUTPUT_GENERIC,'m^2',PERMEABILITY_Z)
#ifdef DASVYAT
      if(realization%discretization%itype == STRUCTURED_GRID_MIMETIC .or. &
         realization%discretization%itype == UNSTRUCTURED_GRID_MIMETIC) then
        call OutputVariableAddToList( &
               realization%output_option%output_variable_list, &
               'Permeability XY',OUTPUT_GENERIC,'m^2',PERMEABILITY_XY)
        call OutputVariableAddToList( &
               realization%output_option%output_variable_list, &
               'Permeability XZ',OUTPUT_GENERIC,'m^2',PERMEABILITY_XZ)
        call OutputVariableAddToList( &
               realization%output_option%output_variable_list, &
               'Permeability YZ',OUTPUT_GENERIC,'m^2',PERMEABILITY_YZ)
      endif
#endif
    else
      call OutputVariableAddToList( &
             realization%output_option%output_variable_list, &
             'Permeability',OUTPUT_GENERIC,'m^2',PERMEABILITY)
    endif
  endif
  if (realization%output_option%print_iproc) then
    output_variable => OutputVariableCreate('Processor ID',OUTPUT_DISCRETE,'', &
                                            PROCESSOR_ID)
    output_variable%plot_only = PETSC_TRUE ! toggle output off for observation
    output_variable%iformat = 1 ! integer
    call OutputVariableAddToList( &
           realization%output_option%output_variable_list,output_variable)
  endif

  if (realization%output_option%print_volume) then
    output_variable => OutputVariableCreate('Volume',OUTPUT_DISCRETE,'', &
                                            VOLUME)
    output_variable%plot_only = PETSC_TRUE ! toggle output off for observation
    output_variable%iformat = 0 ! double
    call OutputVariableAddToList( &
           realization%output_option%output_variable_list,output_variable)
  endif

  if (realization%output_option%print_tortuosity) then
    call OutputVariableAddToList( &
                              realization%output_option%output_variable_list, &
                              'Tortuosity',OUTPUT_GENERIC,'-',TORTUOSITY)
  endif

  ! write material ids
  output_variable => OutputVariableCreate('Material ID',OUTPUT_DISCRETE,'', &
                                          MATERIAL_ID)
  output_variable%plot_only = PETSC_TRUE ! toggle output off for observation
  output_variable%iformat = 1 ! integer
  call OutputVariableAddToList( &
         realization%output_option%output_variable_list,output_variable)  

  
  ! print info
  if (associated(flow_timestepper)) then
    string = 'Flow Stepper:'
    call TimestepperPrintInfo(flow_timestepper,option%fid_out,string,option)
  endif    
  if (associated(tran_timestepper)) then
    string = 'Transport Stepper:'
    call TimestepperPrintInfo(tran_timestepper,option%fid_out,string,option)
  endif    
   if (option%surf_flow_on) then
    string = 'Surface Flow Stepper:'
    call TimestepperPrintInfo(surf_flow_timestepper,option%fid_out,string,option)
  endif

  if (associated(flow_solver)) then
    string = 'Flow Newton Solver:'
    call SolverPrintNewtonInfo(flow_solver,OptionPrintToScreen(option), &
                               OptionPrintToFile(option),option%fid_out, &
                               string)
  endif    
  if (associated(tran_solver)) then
    string = 'Transport Newton Solver:'
    call SolverPrintNewtonInfo(tran_solver,OptionPrintToScreen(option), &
                               OptionPrintToFile(option),option%fid_out, &
                               string)
  endif    
  if (option%ngeomechdof > 0) then
    if (associated(geomech_solver)) then
      string = 'Geomechanics Newton Solver:'
      call SolverPrintNewtonInfo(geomech_solver,OptionPrintToScreen(option), &
                                 OptionPrintToFile(option),option%fid_out, &
                                 string)
    endif
  endif

  if (associated(flow_solver)) then
    string = 'Flow Linear Solver:'
    call SolverPrintLinearInfo(flow_solver,string,option)
  endif    
  if (associated(tran_solver)) then
    string = 'Transport Linear Solver'
    call SolverPrintLinearInfo(tran_solver,string,option)
  endif    
  if (option%ngeomechdof > 0) then
    if (associated(geomech_solver)) then
      string = 'Geomechanics Linear Solver:'
      call SolverPrintLinearInfo(geomech_solver,string,option)
    endif
  endif
  if (associated(surf_flow_solver)) then
    string = 'Surface Flow TS Solver:'
    if (OptionPrintToScreen(option)) then
      write(*,*) ' '
      write(*,*) string
    endif
    call TSView(surf_flow_solver%ts,PETSC_VIEWER_STDOUT_WORLD, &
                ierr);CHKERRQ(ierr)
  endif

  if (debug%print_couplers) then
    call verifyAllCouplers(realization)
  endif
  if (debug%print_waypoints) then
    call WaypointListPrint(realization%waypoint_list,option,realization%output_option)
  endif

#ifdef OS_STATISTICS
  call RealizationPrintGridStatistics(realization)
#endif
  
  ! check for non-initialized data sets, e.g. porosity, permeability
  call RealizationNonInitializedData(realization)

#if defined(PETSC_HAVE_HDF5)
#if !defined(HDF5_BROADCAST)
  call printMsg(option,"Default HDF5 method is used in Initialization")
#else
  call printMsg(option,"Glenn's HDF5 broadcast method is used in Initialization")
#endif
#endif
!PETSC_HAVE_HDF5

  if (option%surf_flow_on) then
    ! Check if surface-flow is compatible with the given flowmode
    select case(option%iflowmode)
      case(RICHARDS_MODE,TH_MODE)
      case default
        option%io_buffer = 'For surface-flow only RICHARDS and TH mode implemented'
        call printErrMsgByRank(option)
    end select

    call SurfaceInitReadRegionFiles(simulation%surf_realization)
    call SurfRealizMapSurfSubsurfGrids(realization,simulation%surf_realization)
    call SurfRealizLocalizeRegions(simulation%surf_realization)
    call SurfRealizPassFieldPtrToPatches(simulation%surf_realization)
    call SurfRealizProcessMatProp(simulation%surf_realization)
    call SurfRealizProcessCouplers(simulation%surf_realization)
    call SurfRealizProcessConditions(simulation%surf_realization)
    !call RealProcessFluidProperties(simulation%surf_realization)
    call SurfaceInitMatPropToRegions(simulation%surf_realization)
    call SurfRealizInitAllCouplerAuxVars(simulation%surf_realization)
    !call SurfaceRealizationPrintCouplers(simulation%surf_realization)

    ! add waypoints associated with boundary conditions, source/sinks etc. to list
    call SurfRealizAddWaypointsToList(simulation%surf_realization)
    call WaypointListFillIn(option,simulation%surf_realization%waypoint_list)
    call WaypointListRemoveExtraWaypnts(option,simulation%surf_realization%waypoint_list)
    if (associated(flow_timestepper)) then
      simulation%surf_flow_timestepper%cur_waypoint => simulation%surf_realization%waypoint_list%first
    endif

    select case(option%iflowmode)
      case(RICHARDS_MODE)
        call SurfaceFlowSetup(simulation%surf_realization)
      case default
      case(TH_MODE)
        call SurfaceTHSetup(simulation%surf_realization)
    end select

    call SurfaceGlobalSetup(simulation%surf_realization)
    ! initialize FLOW
    ! set up auxillary variable arrays

    ! assign initial conditionsRealizAssignFlowInitCond
    call CondControlAssignFlowInitCondSurface(simulation%surf_realization)

    ! override initial conditions if they are to be read from a file
    if (len_trim(option%surf_initialize_flow_filename) > 1) then
      option%io_buffer = 'For surface-flow initial conditions cannot be read from file'
      call printErrMsgByRank(option)
    endif
  
    select case(option%iflowmode)
      case(RICHARDS_MODE)
        call SurfaceFlowUpdateAuxVars(simulation%surf_realization)
      case(TH_MODE)
        call SurfaceTHUpdateAuxVars(surf_realization)
      case default
        option%io_buffer = 'For surface-flow only RICHARDS and TH mode implemented'
        call printErrMsgByRank(option)
    end select
  endif ! option%surf_flow_on

  if (simulation%surf_realization%output_option%print_iproc) then
    output_variable => OutputVariableCreate('Processor ID',OUTPUT_DISCRETE,'', &
                                            PROCESSOR_ID)
    output_variable%plot_only = PETSC_TRUE ! toggle output off for observation
    output_variable%iformat = 1 ! integer
    call OutputVariableAddToList( &
           simulation%surf_realization%output_option%output_variable_list,output_variable)
  endif

  if (option%ngeomechdof > 0) then
    if (option%geomech_subsurf_coupling /= 0) then
      call GeomechCreateGeomechSubsurfVec(simulation%realization, &
                                          simulation%geomech_realization)
      call GeomechCreateSubsurfStressStrainVec(simulation%realization, &
                                               simulation%geomech_realization)

      call GeomechRealizMapSubsurfGeomechGrid(simulation%realization, &
                                              simulation%geomech_realization, &
                                              option)
    endif
    call GeomechRealizLocalizeRegions(simulation%geomech_realization)
    call GeomechRealizPassFieldPtrToPatch(simulation%geomech_realization)
    call GeomechRealizProcessMatProp(simulation%geomech_realization)
    call GeomechRealizProcessGeomechCouplers(simulation%geomech_realization)
    call GeomechRealizProcessGeomechConditions(simulation%geomech_realization)
    call GeomechInitMatPropToGeomechRegions(simulation%geomech_realization)
    call GeomechRealizInitAllCouplerAuxVars(simulation%geomech_realization)  
    call GeomechRealizPrintCouplers(simulation%geomech_realization)  
    call GeomechRealizAddWaypointsToList(simulation%geomech_realization)
    call GeomechGridElemSharedByNodes(geomech_realization)
    call WaypointListFillIn(option,simulation%geomech_realization%waypoint_list)
    call WaypointListRemoveExtraWaypnts(option, &
                                    simulation%geomech_realization%waypoint_list)
    call GeomechForceSetup(simulation%geomech_realization)
    call GeomechGlobalSetup(simulation%geomech_realization)
    
    ! SK: We are solving quasi-steady state solution for geomechanics.
    ! Initial condition is not needed, hence CondControlAssignFlowInitCondGeomech
    ! is not needed, at this point.
    call GeomechForceUpdateAuxVars(simulation%geomech_realization)
  endif

  call printMsg(option," ")
  call printMsg(option,"  Finished Initialization")
  call PetscLogEventEnd(logging%event_init,ierr);CHKERRQ(ierr)

end subroutine Init

! ************************************************************************** !

subroutine InitReadInputFilenames(option,filenames)
  ! 
  ! Reads filenames for multi-simulation runs
  ! 
  ! Author: Glenn Hammond
  ! Date: 08/11/09
  ! 

  use Option_module
  use Input_Aux_module

  type(option_type) :: option
  character(len=MAXSTRINGLENGTH), pointer :: filenames(:)

  character(len=MAXSTRINGLENGTH) :: string
  character(len=MAXSTRINGLENGTH) :: filename
  PetscInt :: filename_count
  type(input_type), pointer :: input
  PetscBool :: card_found

  input => InputCreate(IN_UNIT,option%input_filename,option)

  string = "FILENAMES"
  call InputFindStringInFile(input,option,string) 

  card_found = PETSC_FALSE
  if (InputError(input)) then
    ! if the FILENAMES card is not included, we will assume that only
    ! filenames exist in the file.
    rewind(input%fid)
  else
    card_found = PETSC_TRUE
  endif
    
  filename_count = 0     
  do
    call InputReadPflotranString(input,option)
    if (InputError(input)) exit
    if (InputCheckExit(input,option)) exit  
    call InputReadNChars(input,option,filename,MAXSTRINGLENGTH,PETSC_FALSE)
    filename_count = filename_count + 1
  enddo
  
  allocate(filenames(filename_count))
  filenames = ''
  rewind(input%fid) 

  if (card_found) then
    string = "FILENAMES"
    call InputFindStringInFile(input,option,string) 
  endif
  
  filename_count = 0     
  do
    call InputReadPflotranString(input,option)
    if (InputError(input)) exit
    if (InputCheckExit(input,option)) exit  
    call InputReadNChars(input,option,filename,MAXSTRINGLENGTH,PETSC_FALSE)
    filename_count = filename_count + 1
    filenames(filename_count) = filename
  enddo

  call InputDestroy(input)

end subroutine InitReadInputFilenames

! ************************************************************************** !

subroutine InitReadRequiredCardsFromInput(realization)
  ! 
  ! Reads pflow input file
  ! 
  ! Author: Glenn Hammond
  ! Date: 10/23/07, refactored 08/20/14
  ! 

  use Option_module
  use Discretization_module
  use Grid_module
  use Input_Aux_module
  use String_module
  use Patch_module
  use Realization_class
  use HDF5_Aux_module

  use General_module
  use Reaction_module  
  use Reaction_Aux_module  

  implicit none

  type(realization_type) :: realization

  character(len=MAXSTRINGLENGTH) :: string
  character(len=MAXWORDLENGTH) :: word
  character(len=MAXWORDLENGTH) :: card
  type(patch_type), pointer :: patch, patch2 
  type(grid_type), pointer :: grid
  type(discretization_type), pointer :: discretization
  type(option_type), pointer :: option
  type(input_type), pointer :: input
  
  patch => realization%patch
  option => realization%option
  discretization => realization%discretization
  
  input => realization%input
  
! Read in select required cards
!.........................................................................
  
  ! GRID information - GRID is a required card for every simulation
  string = "GRID"
  call InputFindStringInFile(input,option,string)
  call InputFindStringErrorMsg(input,option,string)

  call DiscretizationReadRequiredCards(discretization,input,option)
  
  select case(discretization%itype)
    case(STRUCTURED_GRID,UNSTRUCTURED_GRID,STRUCTURED_GRID_MIMETIC, &
         UNSTRUCTURED_GRID_MIMETIC)
      patch => PatchCreate()
      patch%grid => discretization%grid
      if (.not.associated(realization%patch_list)) then
        realization%patch_list => PatchCreateList()
      endif
      call PatchAddToList(patch,realization%patch_list)
      realization%patch => patch
  end select

  ! optional required cards - yes, an oxymoron, but we need to know if
  ! these exist before we can go any further.
  rewind(input%fid)  
  do
    call InputReadPflotranString(input,option)
    if (InputError(input)) exit

    call InputReadWord(input,option,word,PETSC_FALSE)
    call StringToUpper(word)
    card = trim(word)

    select case(trim(card))

!....................
      case ('MODE')
        call InputReadWord(input,option,option%flowmode,PETSC_TRUE)
        call InputErrorMsg(input,option,'flowmode','mode')
        select case(trim(option%flowmode))
          case('GENERAL')
            call GeneralRead(input,option)
        end select
  
!....................
      case('DBASE_FILENAME')
        call InputReadWord(input,option,word,PETSC_FALSE)
        call InputErrorMsg(input,option,'filename','DBASE_FILENAME')
        if (index(word,'.h5') > 0) then
#if defined(PETSC_HAVE_HDF5)
          call HDF5ReadDbase(word,option)
#endif
        else
          call InputReadASCIIDbase(word,option)
        endif

!....................
#if defined(SCORPIO)
      case('HDF5_WRITE_GROUP_SIZE')
        call InputReadInt(input,option,option%hdf5_write_group_size)
        call InputErrorMsg(input,option,'HDF5_WRITE_GROUP_SIZE','Group size')
        call InputSkipToEnd(input,option,'HDF5_WRITE_GROUP_SIZE')

      case('HDF5_READ_GROUP_SIZE')
        call InputReadInt(input,option,option%hdf5_read_group_size)
        call InputErrorMsg(input,option,'HDF5_READ_GROUP_SIZE','Group size')
#endif

!....................
      case('PROC')
        ! processor decomposition
        if (realization%discretization%itype == STRUCTURED_GRID .or. &
            realization%discretization%itype == STRUCTURED_GRID_MIMETIC) then
          grid => realization%patch%grid
          ! strip card from front of string
          call InputReadInt(input,option,grid%structured_grid%npx)
          call InputDefaultMsg(input,option,'npx')
          call InputReadInt(input,option,grid%structured_grid%npy)
          call InputDefaultMsg(input,option,'npy')
          call InputReadInt(input,option,grid%structured_grid%npz)
          call InputDefaultMsg(input,option,'npz')
 
          if (option%myrank == option%io_rank .and. &
              option%print_to_screen) then
            option%io_buffer = ' Processor Decomposition:'
            call printMsg(option)
            write(option%io_buffer,'("  npx   = ",3x,i4)') &
              grid%structured_grid%npx
            call printMsg(option)
            write(option%io_buffer,'("  npy   = ",3x,i4)') &
              grid%structured_grid%npy
            call printMsg(option)
            write(option%io_buffer,'("  npz   = ",3x,i4)') &
              grid%structured_grid%npz
            call printMsg(option)
          endif
  
          if (option%mycommsize /= grid%structured_grid%npx * &
                                 grid%structured_grid%npy * &
                                 grid%structured_grid%npz) then
            write(option%io_buffer,*) 'Incorrect number of processors specified: ', &
                           grid%structured_grid%npx*grid%structured_grid%npy* &
                           grid%structured_grid%npz,' commsize = ',option%mycommsize
            call printErrMsg(option)
          endif
        endif
  
!....................
      case('CHEMISTRY')
        !geh: for some reason, we need this with CHEMISTRY read for 
        !     multicontinuum
        option%use_mc = PETSC_TRUE
        call ReactionInit(realization%reaction,input,option)
    end select
  enddo
  
#if defined(SCORPIO)
  call Create_IOGroups(option)
#endif  

end subroutine InitReadRequiredCardsFromInput

! ************************************************************************** !

subroutine InitReadInput(simulation)
  ! 
  ! Reads pflow input file
  ! 
  ! Author: Glenn Hammond
  ! Date: 10/23/07
  ! 

  use Simulation_module
  use Option_module
  use Field_module
  use Grid_module
  use Unstructured_Grid_Aux_module
  use Structured_Grid_module
  use Solver_module
  use Material_module
  use Saturation_Function_module  
  use Characteristic_Curves_module
  use Dataset_Base_class
  use Dataset_module
  use Dataset_Common_HDF5_class
  use Fluid_module
  use Realization_class
  use Timestepper_module
  use Region_module
  use Condition_module
  use Constraint_module
  use Coupler_module
  use Strata_module
  use Observation_module
  use Waypoint_module
  use Debug_module
  use Patch_module
  use Reaction_module
  use Reaction_Aux_module
  use Discretization_module
  use Input_Aux_module
  use String_module
  use Units_module
  use Uniform_Velocity_module
  use Mineral_module
  use Regression_module
  use Output_Aux_module
  use Output_Tecplot_module
  use Mass_Transfer_module
  use EOS_module
  use EOS_Water_module
  use SrcSink_Sandbox_module
  
  use Surface_Flow_module
  use Surface_Init_module, only : SurfaceInitReadInput
  use Geomechanics_Init_module, only : GeomechanicsInitReadInput
  use Geomechanics_Realization_class
#ifdef SOLID_SOLUTION
  use Solid_Solution_module, only : SolidSolutionReadFromInputFile
#endif
 
  use Root_Module

  implicit none
  
  type(simulation_type) :: simulation

  PetscErrorCode :: ierr
  character(len=MAXWORDLENGTH) :: word
  character(len=MAXWORDLENGTH) :: card
    
  PetscBool :: continuation_flag
  
  character(len=1) :: backslash
  PetscReal :: temp_real, temp_real2
  PetscReal :: units_conversion
  PetscInt :: temp_int
  PetscInt :: count, id
  
  PetscBool :: vel_cent
  PetscBool :: vel_face
  PetscBool :: fluxes
  PetscBool :: mass_flowrate
  PetscBool :: energy_flowrate
  PetscBool :: aveg_mass_flowrate
  PetscBool :: aveg_energy_flowrate
  
  type(region_type), pointer :: region
  type(flow_condition_type), pointer :: flow_condition
  type(tran_condition_type), pointer :: tran_condition
  type(tran_constraint_type), pointer :: tran_constraint
  type(tran_constraint_type), pointer :: sec_tran_constraint
  type(coupler_type), pointer :: coupler
  type(strata_type), pointer :: strata
  type(observation_type), pointer :: observation
  
  type(waypoint_type), pointer :: waypoint
  
  type(material_property_type), pointer :: material_property
  type(fluid_property_type), pointer :: fluid_property
  type(saturation_function_type), pointer :: saturation_function
  class(characteristic_curves_type), pointer :: characteristic_curves

  type(realization_type), pointer :: realization
  type(grid_type), pointer :: grid
  type(option_type), pointer :: option
  type(field_type), pointer :: field
  type(patch_type), pointer :: patch   
  type(solver_type), pointer :: flow_solver
  type(solver_type), pointer :: tran_solver
  type(solver_type), pointer :: default_solver
  type(solver_type), pointer :: solver_pointer
  type(timestepper_type), pointer :: flow_timestepper
  type(timestepper_type), pointer :: tran_timestepper
  type(timestepper_type), pointer :: default_timestepper
  type(reaction_type), pointer :: reaction
  type(output_option_type), pointer :: output_option
  type(uniform_velocity_dataset_type), pointer :: uniform_velocity_dataset
  class(dataset_base_type), pointer :: dataset
  type(mass_transfer_type), pointer :: flow_mass_transfer
  type(mass_transfer_type), pointer :: rt_mass_transfer
  type(input_type), pointer :: input
  type(geomech_realization_type), pointer :: geomech_realization

  type(root_property_type), pointer :: root_property
  PetscInt :: size
  PetscInt, pointer :: tmp_int(:), tmp_int2(:)

  nullify(flow_solver)
  nullify(tran_solver)
  
  realization => simulation%realization
  patch => realization%patch
  
  geomech_realization => simulation%geomech_realization

  if (associated(patch)) grid => patch%grid

  option => realization%option
  output_option => realization%output_option
  field => realization%field
  reaction => realization%reaction
  input => realization%input

  tran_timestepper => simulation%tran_timestepper
  if (associated(tran_timestepper)) then
    tran_solver => tran_timestepper%solver
    tran_solver%itype = TRANSPORT_CLASS
  endif
  flow_timestepper => simulation%flow_timestepper
  if (associated(flow_timestepper)) then
    flow_solver => flow_timestepper%solver
    flow_solver%itype = FLOW_CLASS
  endif

  if (associated(flow_timestepper)) then
    default_timestepper => flow_timestepper
    default_solver => flow_solver
  else
    default_timestepper => tran_timestepper
    default_solver => tran_solver
  endif

  backslash = achar(92)  ! 92 = "\" Some compilers choke on \" thinking it
                          ! is a double quote as in c/c++
                              
  rewind(input%fid)  
      
  do
    call InputReadPflotranString(input,option)
    if (InputError(input)) exit

    call InputReadWord(input,option,word,PETSC_FALSE)
    call StringToUpper(word)
    card = trim(word)

    option%io_buffer = 'pflotran card:: ' // trim(card)
    call printMsg(option)

    select case(trim(card))

!....................
      case ('MODE')
         call InputReadWord(input, option, word, PETSC_FALSE)
         call StringToUpper(word)
         if ('TH' == trim(word)) then
            call InputReadWord(input, option, word, PETSC_TRUE)
            call InputErrorMsg(input, option, 'th freezing mode', 'mode th')
            call StringToUpper(word)
            if ('FREEZING' == trim(word)) then
               option%use_th_freezing = PETSC_TRUE
               option%io_buffer = ' TH: using FREEZING submode!'
               call printMsg(option)
               ! Override the default setting for TH-mode with freezing
               call EOSWaterSetDensityPainter()
               call EOSWaterSetEnthalpyPainter()
            else if ('NO_FREEZING' == trim(word)) then
               option%use_th_freezing = PETSC_FALSE
               option%io_buffer = ' TH: using NO_FREEZING submode!'
               call printMsg(option)
            else
               ! NOTE(bja, 2013-12) use_th_freezing defaults to false, can skip this....
               option%io_buffer = ' TH: must specify FREEZING or NO_FREEZING submode!'
               call printErrMsg(option)
            endif
         else if (trim(word) == 'GENERAL') then
           call InputReadWord(input, option, word, PETSC_TRUE)
           if (input%ierr == 0) then
             call InputSkipToEnd(input,option,card)
           endif
         endif  
        
!....................
      case ('ICE_MODEL')
        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case (trim(word))
          case ('PAINTER_EXPLICIT')
            option%ice_model = PAINTER_EXPLICIT
          case ('PAINTER_KARRA_IMPLICIT')
            option%ice_model = PAINTER_KARRA_IMPLICIT
          case ('PAINTER_KARRA_EXPLICIT')
            option%ice_model = PAINTER_KARRA_EXPLICIT
          case ('PAINTER_KARRA_EXPLICIT_NOCRYO')
            option%ice_model = PAINTER_KARRA_EXPLICIT_NOCRYO
          case ('DALL_AMICO')
            option%ice_model = DALL_AMICO
          case default
            option%io_buffer = 'Cannot identify the specificed ice model.' // &
             'Specify PAINTER_EXPLICIT or PAINTER_KARRA_IMPLICIT' // &
             ' or PAINTER_KARRA_EXPLICIT.'
          end select

!....................
      case ('RELATIVE_PERMEABILITY_AVERAGE')
        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case (trim(word))
          case ('UPWIND')
            option%rel_perm_aveg = UPWIND
          case ('HARMONIC')
            option%rel_perm_aveg = HARMONIC
          case ('DYNAMIC_HARMONIC')
            option%rel_perm_aveg = DYNAMIC_HARMONIC
          case default
            option%io_buffer = 'Cannot identify the specificed ' // &
              'RELATIVE_PERMEABILITY_AVERAGE.'
            call printErrMsg(option)
          end select

!....................
      case ('GRID')
        call DiscretizationRead(realization%discretization,input,option)

!....................
      case ('CHEMISTRY')
        call ReactionReadPass2(reaction,input,option)

!....................
      case ('NONUNIFORM_VELOCITY')
        call InputReadNChars(input,option, &
                             realization%nonuniform_velocity_filename, &
                             MAXSTRINGLENGTH,PETSC_TRUE)
        call InputErrorMsg(input,option,'filename','NONUNIFORM_VELOCITY')

      case ('UNIFORM_VELOCITY')
        uniform_velocity_dataset => UniformVelocityDatasetCreate()
        uniform_velocity_dataset%rank = 3
        uniform_velocity_dataset%interpolation_method = 1 ! 1 = STEP
        uniform_velocity_dataset%is_cyclic = PETSC_FALSE
        allocate(uniform_velocity_dataset%times(1))
        uniform_velocity_dataset%times = 0.d0
        allocate(uniform_velocity_dataset%values(3,1))
        uniform_velocity_dataset%values = 0.d0
        call InputReadDouble(input,option,uniform_velocity_dataset%values(1,1))
        call InputErrorMsg(input,option,'velx','UNIFORM_VELOCITY')
        call InputReadDouble(input,option,uniform_velocity_dataset%values(2,1))
        call InputErrorMsg(input,option,'vely','UNIFORM_VELOCITY')
        call InputReadDouble(input,option,uniform_velocity_dataset%values(3,1))
        call InputErrorMsg(input,option,'velz','UNIFORM_VELOCITY')
        ! read units, if present
        call InputReadWord(input,option,word,PETSC_TRUE)
        if (input%ierr == 0) then
          units_conversion = UnitsConvertToInternal(word,option) 
          uniform_velocity_dataset%values(:,1) = &
            uniform_velocity_dataset%values(:,1) * units_conversion
        endif
        call UniformVelocityDatasetVerify(option,uniform_velocity_dataset)
        realization%uniform_velocity_dataset => uniform_velocity_dataset
      
      case ('VELOCITY_DATASET')
        uniform_velocity_dataset => UniformVelocityDatasetCreate()
        call UniformVelocityDatasetRead(uniform_velocity_dataset,input,option)
        realization%uniform_velocity_dataset => uniform_velocity_dataset

!....................
      case ('DEBUG')
        call DebugRead(realization%debug,input,option)
               
!....................
      case ('PRINT_PRIMAL_GRID')
        option%print_explicit_primal_grid = PETSC_TRUE
        
!....................
      case ('PRINT_DUAL_GRID')
        option%print_explicit_dual_grid = PETSC_TRUE

!....................
      case ('MAX_CHANGE')
        call InputReadDouble(input,option,option%dpmxe)
        call InputErrorMsg(input,option,'dpmxe','MAX_CHANGE')
        call InputReadDouble(input,option,option%dtmpmxe)
        call InputErrorMsg(input,option,'dtmpmxe','MAX_CHANGE')
        call InputReadDouble(input,option,option%dsmxe)
        call InputErrorMsg(input,option,'dsmxe','MAX_CHANGE')
        call InputReadDouble(input,option,option%dcmxe)
        call InputErrorMsg(input,option,'dcmxe','MAX_CHANGE')
        
!....................
      case ('PROC')
      
!....................
      case ('REGION')
        region => RegionCreate()
        call InputReadWord(input,option,region%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'name','REGION') 
        call printMsg(option,region%name)
        call RegionRead(region,input,option)
        ! we don't copy regions down to patches quite yet, since we
        ! don't want to duplicate IO in reading the regions
        call RegionAddToList(region,realization%regions)   
        nullify(region)   

!....................
      case ('FLOW_CONDITION')
        flow_condition => FlowConditionCreate(option)
        call InputReadWord(input,option,flow_condition%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'FLOW_CONDITION','name') 
        call printMsg(option,flow_condition%name)
        if (option%iflowmode == G_MODE) then
          call FlowConditionGeneralRead(flow_condition,input,option)
        else
          call FlowConditionRead(flow_condition,input,option)
        endif
        call FlowConditionAddToList(flow_condition,realization%flow_conditions)
        nullify(flow_condition)
        
!....................
      case ('TRANSPORT_CONDITION')
        if (.not.associated(reaction)) then
          option%io_buffer = 'TRANSPORT_CONDITIONs not supported without ' // &
            'CHEMISTRY.'
          call printErrMsg(option)
        endif
        tran_condition => TranConditionCreate(option)
        call InputReadWord(input,option,tran_condition%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'TRANSPORT_CONDITION','name') 
        call printMsg(option,tran_condition%name)
        call TranConditionRead(tran_condition,realization%transport_constraints, &
                               reaction,input,option)
        call TranConditionAddToList(tran_condition,realization%transport_conditions)
        nullify(tran_condition)

!....................
      case('CONSTRAINT')
        if (.not.associated(reaction)) then
          option%io_buffer = 'CONSTRAINTs not supported without CHEMISTRY.'
          call printErrMsg(option)
        endif
        tran_constraint => TranConstraintCreate(option)
        call InputReadWord(input,option,tran_constraint%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'constraint','name') 
        call printMsg(option,tran_constraint%name)
        call TranConstraintRead(tran_constraint,reaction,input,option)
        call TranConstraintAddToList(tran_constraint,realization%transport_constraints)
        nullify(tran_constraint)


!....................
      case ('BOUNDARY_CONDITION')
        coupler => CouplerCreate(BOUNDARY_COUPLER_TYPE)
        call InputReadWord(input,option,coupler%name,PETSC_TRUE)
        call InputDefaultMsg(input,option,'Boundary Condition name') 
        call CouplerRead(coupler,input,option)
        call RealizationAddCoupler(realization,coupler)
        nullify(coupler)
      
!....................
      case ('INITIAL_CONDITION')
        coupler => CouplerCreate(INITIAL_COUPLER_TYPE)
        call InputReadWord(input,option,coupler%name,PETSC_TRUE)
        call InputDefaultMsg(input,option,'Initial Condition name') 
        call CouplerRead(coupler,input,option)
        call RealizationAddCoupler(realization,coupler)
        nullify(coupler)
      
!....................
      case ('SOURCE_SINK')
        coupler => CouplerCreate(SRC_SINK_COUPLER_TYPE)
        call InputReadWord(input,option,coupler%name,PETSC_TRUE)
        call InputDefaultMsg(input,option,'Source Sink name') 
        call CouplerRead(coupler,input,option)
        call RealizationAddCoupler(realization,coupler)
        nullify(coupler)        
      
!....................
      case ('SOURCE_SINK_SANDBOX')
        call SSSandboxInit(option)
        call SSSandboxRead(input,option)
      
!....................
      case ('FLOW_MASS_TRANSFER')
        flow_mass_transfer => MassTransferCreate()
        call InputReadWord(input,option,flow_mass_transfer%name,PETSC_TRUE)
        call InputDefaultMsg(input,option,'Flow Mass Transfer name') 
        call MassTransferRead(flow_mass_transfer,input,option)
        call MassTransferAddToList(flow_mass_transfer, &
                                   realization%flow_mass_transfer_list)
        nullify(flow_mass_transfer)
      
!....................
      case ('RT_MASS_TRANSFER')
        rt_mass_transfer => MassTransferCreate()
        call InputReadWord(input,option,rt_mass_transfer%name,PETSC_TRUE)
        call InputDefaultMsg(input,option,'RT Mass Transfer name')
        call MassTransferRead(rt_mass_transfer,input,option)
        call MassTransferAddToList(rt_mass_transfer, &
                                   realization%rt_mass_transfer_list)
        nullify(rt_mass_transfer)
      
!....................
      case ('STRATIGRAPHY','STRATA')
        strata => StrataCreate()
        call StrataRead(strata,input,option)
        call RealizationAddStrata(realization,strata)
        nullify(strata)

!.....................
      case ('DATASET')
        nullify(dataset)
        call DatasetRead(input,dataset,option)
        call DatasetBaseAddToList(dataset,realization%datasets)
        nullify(dataset)
        
!....................

      case('REFERENCE_PRESSURE')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%reference_pressure)
        call InputDefaultMsg(input,option,'Reference Pressure') 

!....................

      case('REFERENCE_DENSITY')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%reference_water_density)
        call InputDefaultMsg(input,option,'Reference Density') 

!....................

      case('MINIMUM_HYDROSTATIC_PRESSURE')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%minimum_hydrostatic_pressure)
        call InputDefaultMsg(input,option,'Minimum Hydrostatic Pressure') 

!......................

      case('REFERENCE_TEMPERATURE')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%reference_temperature)
        call InputDefaultMsg(input,option,'Reference Temperature') 

!......................

      case('ANI_RELATIVE_PERMEABILTY')
        option%ani_relative_permeability = PETSC_TRUE

      case('REFERENCE_POROSITY')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%reference_porosity)
        call InputDefaultMsg(input,option,'Reference Porosity') 

!......................

      case('REFERENCE_SATURATION')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%reference_saturation)
        call InputDefaultMsg(input,option,'Reference Saturation') 

!......................

      case('NONISOTHERMAL')
        option%use_isothermal = PETSC_FALSE

!......................

      case('ISOTHERMAL')
        option%use_isothermal = PETSC_TRUE
        
!......................

      case('UPDATE_FLOW_PERMEABILITY')
        option%update_flow_perm = PETSC_TRUE
        
!......................

      case('DFN')
        grid%unstructured_grid%grid_type = TWO_DIM_GRID    
            
!......................

      case("MULTIPLE_CONTINUUM")
        option%use_mc = PETSC_TRUE
              
!......................

      case('SECONDARY_CONTINUUM_SOLVER')
        if (.not.option%use_mc) then
          option%io_buffer = 'SECONDARY_CONTINUUM_SOLVER can only be used ' // &
                             'with MULTIPLE_CONTINUUM keyword.'
          call printErrMsg(option)
        endif      
        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case(word)
          case('KEARST')
            option%secondary_continuum_solver = 1
          case('HINDMARSH')
            option%secondary_continuum_solver = 2
          case('THOMAS')
            option%secondary_continuum_solver = 3
          case default
            option%io_buffer = 'SECONDARY_CONTINUUM_SOLVER can be only ' // &
                               'HINDMARSH or KEARST. For single component'// &
                               'chemistry THOMAS can be used.'
          call printErrMsg(option)    
        end select        
!....................

      case('SECONDARY_CONSTRAINT')
        if (.not.option%use_mc) then
          option%io_buffer = 'SECONDARY_CONSTRAINT can only be used with ' // &
                             'MULTIPLE_CONTINUUM keyword.'
          call printErrMsg(option)
        endif
        if (.not.associated(reaction)) then
          option%io_buffer = 'SECONDARY_CONSTRAINT not supported without' // &
                             'CHEMISTRY.'
          call printErrMsg(option)
        endif
        sec_tran_constraint => TranConstraintCreate(option)
        call InputReadWord(input,option,sec_tran_constraint%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'secondary constraint','name') 
        call printMsg(option,sec_tran_constraint%name)
        call TranConstraintRead(sec_tran_constraint,reaction,input,option)
        realization%sec_transport_constraint => sec_tran_constraint
        nullify(sec_tran_constraint)        

!......................

      case('BRIN','BRINE')
        call InputReadStringErrorMsg(input,option,card)
        call InputReadDouble(input,option,option%m_nacl)
        call InputDefaultMsg(input,option,'NaCl Concentration') 

        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case(word(1:len_trim(word)))
          case('MOLAL')
          case('MASS')
            option%m_nacl = option%m_nacl /FMWNACL/(1.D0-option%m_nacl)
          case('MOLE')    
            option%m_nacl = option%m_nacl /FMWH2O/(1.D0-option%m_nacl)
          case default
            print *, 'Wrong unit: ', word(1:len_trim(word))
            stop
         end select 
         if (OptionPrintToScreen(option)) print *, option%m_nacl
!......................

      case ('RESTART')
        option%restart_flag = PETSC_TRUE
        call InputReadNChars(input,option,option%restart_filename,MAXSTRINGLENGTH, &
                             PETSC_TRUE)
        call InputErrorMsg(input,option,'RESTART','Restart file name') 
        call InputReadDouble(input,option,option%restart_time)
        if (input%ierr == 0) then
          call InputReadWord(input,option,word,PETSC_TRUE)
          if (input%ierr == 0) then
            option%restart_time = option%restart_time* &
                                  UnitsConvertToInternal(word,option)
          else
            call InputDefaultMsg(input,option,'RESTART, time units')
          endif
        endif

!......................

      case ('CHECKPOINT')
        option%checkpoint_flag = PETSC_TRUE
        call InputReadInt(input,option,option%checkpoint_frequency)

        if (input%ierr == 1) then
          option%checkpoint_frequency = 0
          do
            call InputReadPflotranString(input,option)
            call InputReadStringErrorMsg(input,option,card)
            if (InputCheckExit(input,option)) exit

            call InputReadWord(input,option,word,PETSC_TRUE)
            call InputErrorMsg(input,option,'keyword','CHECKPOINT')
            call StringToUpper(word)

            select case(trim(word))
              case ('PERIODIC')
                call InputReadWord(input,option,word,PETSC_TRUE)
                call InputErrorMsg(input,option,'time increment', &
                                   'OUTPUT,PERIODIC')
                call StringToUpper(word)

                select case(trim(word))
                  case('TIME')
                    call InputReadDouble(input,option,temp_real)
                    call InputErrorMsg(input,option,'time increment', &
                                       'CHECKPOINT,PERIODIC,TIME')
                    call InputReadWord(input,option,word,PETSC_TRUE)
                    call InputErrorMsg(input,option,'time increment units', &
                                       'CHECKPOINT,PERIODIC,TIME')
                    units_conversion = UnitsConvertToInternal(word,option)
                    output_option%periodic_checkpoint_time_incr = temp_real* &
                                                              units_conversion
                  case('TIMESTEP')
                    call InputReadInt(input,option,option%checkpoint_frequency)
                    call InputErrorMsg(input,option,'timestep increment', &
                                       'CHECKPOINT,PERIODIC,TIMESTEP')
                  case default
                    option%io_buffer = 'Keyword: ' // trim(word) // &
                                       ' not recognized in CHECKPOINT,PERIODIC.'
                    call printErrMsg(option)
                end select
              case default
                option%io_buffer = 'Keyword: ' // trim(word) // &
                                   ' not recognized in CHECKPOINT.'
                call printErrMsg(option)
            end select
          enddo
          if (output_option%periodic_checkpoint_time_incr /= 0.d0 .and. &
              option%checkpoint_frequency /= 0) then
            option%io_buffer = 'Both TIME and TIMESTEP cannot be specified ' // &
              'for CHECKPOINT,PERIODIC.'
            call printErrMsg(option)
          endif
          if (output_option%periodic_checkpoint_time_incr == 0.d0 .and. &
              option%checkpoint_frequency == 0) then
            option%io_buffer = 'Either, TIME and TIMESTEP need to be specified ' // &
              'for CHECKPOINT,PERIODIC.'
            call printErrMsg(option)
          endif
        endif

!......................

      case ('NUMERICAL_JACOBIAN_FLOW')
        option%numerical_derivatives_flow = PETSC_TRUE

!......................

      case ('NUMERICAL_JACOBIAN_RXN')
        option%numerical_derivatives_rxn = PETSC_TRUE

!......................

      case ('NUMERICAL_JACOBIAN_MULTI_COUPLE')
        option%numerical_derivatives_multi_coupling = PETSC_TRUE

!......................

      case ('COMPUTE_STATISTICS')
        option%compute_statistics = PETSC_TRUE

!....................

      case ('CO2_DATABASE')
        call InputReadNChars(input,option,option%co2_database_filename, &
                             MAXSTRINGLENGTH,PETSC_TRUE)
        call InputErrorMsg(input,option,'CO2_DATABASE','filename')
        
!....................

      case ('TIMESTEPPER')
        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case(word)
          case('FLOW')
            if (associated(flow_solver)) then
              call TimestepperRead(flow_timestepper,input,option)
            else
              call InputSkipToEnd(input,option,card)
            endif
          case('TRAN','TRANSPORT')
            if (associated(tran_solver)) then
              call TimestepperRead(tran_timestepper,input,option)
            else
              call InputSkipToEnd(input,option,card)
            endif
          case default
            if (associated(default_timestepper)) then
              call TimestepperRead(default_timestepper,input,option)
            else
              call InputSkipToEnd(input,option,card)
            endif
        end select

!....................

      case ('LINEAR_SOLVER')
        nullify(solver_pointer)
        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case(word)
          case('FLOW')
            solver_pointer => flow_solver
          case('TRAN','TRANSPORT')
            solver_pointer => tran_solver
          case default
            solver_pointer => default_solver
        end select
        if (associated(solver_pointer)) then
          call SolverReadLinear(solver_pointer,input,option)
        else
          call InputSkipToEnd(input,option,card)
        endif

!....................

      case ('NEWTON_SOLVER')
        nullify(solver_pointer)
        call InputReadWord(input,option,word,PETSC_FALSE)
        call StringToUpper(word)
        select case(word)
          case('FLOW')
            solver_pointer => flow_solver
          case('TRAN','TRANSPORT')
            solver_pointer => tran_solver
          case default
            solver_pointer => default_solver
        end select
        if (associated(solver_pointer)) then
          call SolverReadNewton(solver_pointer,input,option)
        else
          call InputSkipToEnd(input,option,card)
        endif

        if (associated(solver_pointer,flow_solver) .and. &
            solver_pointer%check_post_convergence) then
          option%flow%check_post_convergence = PETSC_TRUE
          option%flow%inf_scaled_res_tol = &
            solver_pointer%newton_inf_scaled_res_tol
          option%flow%inf_rel_update_tol = &
            solver_pointer%newton_inf_rel_update_tol
        endif
        if (associated(solver_pointer,tran_solver) .and. &
            solver_pointer%check_post_convergence) then
          option%transport%check_post_convergence = PETSC_TRUE
          option%transport%inf_scaled_res_tol = &
            solver_pointer%newton_inf_scaled_res_tol
          option%transport%inf_rel_update_tol = &
            solver_pointer%newton_inf_rel_update_tol
        endif
        
!....................

      case ('FLUID_PROPERTY')

        fluid_property => FluidPropertyCreate()
        call FluidPropertyRead(fluid_property,input,option)
        call FluidPropertyAddToList(fluid_property,realization%fluid_properties)
        nullify(fluid_property)
        
!....................

      case ('EOS')
        call EOSRead(input,option)

!....................

      case ('SATURATION_FUNCTION')
      
        saturation_function => SaturationFunctionCreate(option)
        call InputReadWord(input,option,saturation_function%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'name','SATURATION_FUNCTION')
        call SaturationFunctionRead(saturation_function,input,option)
        call SatFunctionComputePolynomial(option,saturation_function)
        call PermFunctionComputePolynomial(option,saturation_function)
        call SaturationFunctionAddToList(saturation_function, &
                                         realization%saturation_functions)
        nullify(saturation_function)   

!....................

      case ('CHARACTERISTIC_CURVES')
      
        characteristic_curves => CharacteristicCurvesCreate()
        call InputReadWord(input,option,characteristic_curves%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'name','CHARACTERISTIC_CURVES')
        call CharacteristicCurvesRead(characteristic_curves,input,option)
!        call SatFunctionComputePolynomial(option,saturation_function)
!        call PermFunctionComputePolynomial(option,saturation_function)
        call CharacteristicCurvesAddToList(characteristic_curves, &
                                          realization%characteristic_curves)
        nullify(characteristic_curves)   

!....................
      
      case ('MATERIAL_PROPERTY')

        material_property => MaterialPropertyCreate()
        call InputReadWord(input,option,material_property%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'name','MATERIAL_PROPERTY')        
        call MaterialPropertyRead(material_property,input,option)
        call MaterialPropertyAddToList(material_property,realization%material_properties)
        nullify(material_property)

!....................

      case ('USE_TOUCH_OPTIONS')
        option%use_touch_options = PETSC_TRUE

      case ('MPI_IO')
!        call PetscOptionsInsertString('-viewer_binary_mpiio')

      case ('HANDSHAKE_IO')
        call InputReadInt(input,option,option%io_handshake_buffer_size)
        call InputErrorMsg(input,option,'io_handshake_buffer_size','HANDSHAKE_IO')

      case ('OVERWRITE_RESTART_TRANSPORT')
        option%overwrite_restart_transport = PETSC_TRUE

      case ('OVERWRITE_RESTART_FLOW_PARAMS')
        option%overwrite_restart_flow = PETSC_TRUE

      case ('INITIALIZE_FLOW_FROM_FILE')
        call InputReadNChars(input,option,option%initialize_flow_filename, &
                             MAXSTRINGLENGTH,PETSC_TRUE)
        call InputErrorMsg(input,option,'filename','INITIALIZE_FLOW_FROM_FILE') 

      case ('INITIALIZE_TRANSPORT_FROM_FILE')
        call InputReadNChars(input,option,option%initialize_transport_filename, &
                             MAXSTRINGLENGTH,PETSC_TRUE)
        call InputErrorMsg(input,option,'filename','INITIALIZE_TRANSPORT_FROM_FILE') 

      case ('CENTRAL_DIFFERENCE')
        option%use_upwinding = PETSC_FALSE

!....................
      case ('OBSERVATION')
        observation => ObservationCreate()
        call ObservationRead(observation,input,option)
        call RealizationAddObservation(realization,observation)        
      
!.....................
      case ('WALLCLOCK_STOP')
        option%wallclock_stop_flag = PETSC_TRUE
        call InputReadDouble(input,option,option%wallclock_stop_time)
        call InputErrorMsg(input,option,'stop time','WALLCLOCK_STOP') 

        call InputReadWord(input,option,word,PETSC_TRUE)
        if (input%ierr /= 0) word = 'h'
        call InputDefaultMsg(input,option,'WALLCLOCK_STOP time units')
        units_conversion = UnitsConvertToInternal(word,option) 
        ! convert from hrs to seconds and add to start_time
        option%wallclock_stop_time = option%start_time + &
                                     option%wallclock_stop_time*units_conversion
      
!....................
      case ('OUTPUT')
        vel_cent = PETSC_FALSE
        vel_face = PETSC_FALSE
        fluxes = PETSC_FALSE
        mass_flowrate = PETSC_FALSE
        energy_flowrate = PETSC_FALSE
        aveg_mass_flowrate = PETSC_FALSE
        aveg_energy_flowrate = PETSC_FALSE
        do
          call InputReadPflotranString(input,option)
          call InputReadStringErrorMsg(input,option,card)
          if (InputCheckExit(input,option)) exit
          call InputReadWord(input,option,word,PETSC_TRUE)
          call InputErrorMsg(input,option,'keyword','OUTPUT') 
          call StringToUpper(word)
          select case(trim(word))
            case('NO_FINAL','NO_PRINT_FINAL')
              output_option%print_final = PETSC_FALSE
            case('NO_INITIAL','NO_PRINT_INITIAL')
              output_option%print_initial = PETSC_FALSE
            case('PROCESSOR_ID')
              output_option%print_iproc = PETSC_TRUE
            case('PERMEABILITY')
              output_option%print_permeability = PETSC_TRUE
            case('POROSITY')
              output_option%print_porosity = PETSC_TRUE
            case('TORTUOSITY')
              output_option%print_tortuosity = PETSC_TRUE
            case('MASS_BALANCE')
              option%compute_mass_balance_new = PETSC_TRUE
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputDefaultMsg(input,option, &
                                 'MASS_BALANCE,DETAILED,OUTPUT')
              if (len_trim(word) > 0) then
                call StringToUpper(word)
                select case(trim(word))
                  case('DETAILED')
                    option%mass_bal_detailed = PETSC_TRUE
                  case('DEFAULT')
                    option%io_buffer = 'Keyword: ' // trim(word) // &
                      ' not recognized in OUTPUT,'// &
                      'MASS_BALANCE,DETAILED.'
                    call printErrMsg(option)
                end select
              endif
            case('PRINT_COLUMN_IDS')
              output_option%print_column_ids = PETSC_TRUE
            case('TIMES')
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'units','OUTPUT')
              units_conversion = UnitsConvertToInternal(word,option) 
              continuation_flag = PETSC_TRUE
              do
                continuation_flag = PETSC_FALSE
                if (index(input%buf,backslash) > 0) &
                  continuation_flag = PETSC_TRUE
                input%ierr = 0
                do
                  if (InputError(input)) exit
                  call InputReadDouble(input,option,temp_real)
                  if (.not.InputError(input)) then
                    waypoint => WaypointCreate()
                    waypoint%time = temp_real*units_conversion
                    waypoint%print_output = PETSC_TRUE    
                    call WaypointInsertInList(waypoint,realization%waypoint_list)
                  endif
                enddo
                if (.not.continuation_flag) exit
                call InputReadPflotranString(input,option)
                if (InputError(input)) exit
              enddo
            case('OUTPUT_FILE')
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'time increment', &
                                 'OUTPUT,OUTPUT_FILE')
              call StringToUpper(word)
              select case(trim(word))
                case('OFF')
                  option%print_to_file = PETSC_FALSE
                case('PERIODIC')
                  call InputReadInt(input,option,output_option%output_file_imod)
                  call InputErrorMsg(input,option,'timestep increment', &
                                     'OUTPUT,PERIODIC,OUTPUT_FILE')
                case default
                  option%io_buffer = 'Keyword: ' // trim(word) // &
                                     ' not recognized in OUTPUT,OUTPUT_FILE.'
                  call printErrMsg(option)
              end select
            case('SCREEN')
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'time increment','OUTPUT,SCREEN')
              call StringToUpper(word)
              select case(trim(word))
                case('OFF')
                  option%print_to_screen = PETSC_FALSE
                case('PERIODIC')
                  call InputReadInt(input,option,output_option%screen_imod)
                  call InputErrorMsg(input,option,'timestep increment', &
                                     'OUTPUT,PERIODIC,SCREEN')
                case default
                  option%io_buffer = 'Keyword: ' // trim(word) // &
                                     ' not recognized in OUTPUT,SCREEN.'
                  call printErrMsg(option)
              end select
            case('PERIODIC')
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'time increment', &
                                 'OUTPUT,PERIODIC')
              call StringToUpper(word)
              select case(trim(word))
                case('TIME')
                  call InputReadDouble(input,option,temp_real)
                  call InputErrorMsg(input,option,'time increment', &
                                     'OUTPUT,PERIODIC,TIME')
                  call InputReadWord(input,option,word,PETSC_TRUE)
                  call InputErrorMsg(input,option,'time increment units', &
                                     'OUTPUT,PERIODIC,TIME')
                  units_conversion = UnitsConvertToInternal(word,option) 
                  output_option%periodic_output_time_incr = temp_real* &
                                                            units_conversion
                  call InputReadWord(input,option,word,PETSC_TRUE)
                  if (input%ierr == 0) then
                    if (StringCompareIgnoreCase(word,'between')) then

                      call InputReadDouble(input,option,temp_real)
                      call InputErrorMsg(input,option,'start time', &
                                         'OUTPUT,PERIODIC,TIME')
                      call InputReadWord(input,option,word,PETSC_TRUE)
                      call InputErrorMsg(input,option,'start time units', &
                                         'OUTPUT,PERIODIC,TIME')
                      units_conversion = UnitsConvertToInternal(word,option) 
                      temp_real = temp_real * units_conversion
                      call InputReadWord(input,option,word,PETSC_TRUE)
                      if (.not.StringCompareIgnoreCase(word,'and')) then
                        input%ierr = 1
                      endif
                      call InputErrorMsg(input,option,'and', &
                                          'OUTPUT,PERIODIC,TIME"')
                      call InputReadDouble(input,option,temp_real2)
                      call InputErrorMsg(input,option,'end time', &
                                         'OUTPUT,PERIODIC,TIME')
                      call InputReadWord(input,option,word,PETSC_TRUE)
                      call InputErrorMsg(input,option,'end time units', &
                                         'OUTPUT,PERIODIC,TIME')
                      temp_real2 = temp_real2 * units_conversion
                      do
                        waypoint => WaypointCreate()
                        waypoint%time = temp_real
                        waypoint%print_output = PETSC_TRUE    
                        call WaypointInsertInList(waypoint,realization%waypoint_list)
                        temp_real = temp_real + output_option%periodic_output_time_incr
                        if (temp_real > temp_real2) exit
                      enddo
                      output_option%periodic_output_time_incr = 0.d0
                    else
                      input%ierr = 1
                      call InputErrorMsg(input,option,'between', &
                                          'OUTPUT,PERIODIC,TIME')
                    endif
                  endif                  
                case('TIMESTEP')
                  call InputReadInt(input,option, &
                                    output_option%periodic_output_ts_imod)
                  call InputErrorMsg(input,option,'timestep increment', &
                                     'OUTPUT,PERIODIC,TIMESTEP')
                case default
                  option%io_buffer = 'Keyword: ' // trim(word) // &
                                     ' not recognized in OUTPUT,PERIODIC,'// &
                                     'TIMESTEP.'
                  call printErrMsg(option)
              end select
            case('PERIODIC_OBSERVATION')
              output_option%print_observation = PETSC_TRUE
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'time increment', &
                'OUTPUT, PERIODIC_OBSERVATION')
              call StringToUpper(word)
              select case(trim(word))
                case('TIME')
                  call InputReadDouble(input,option,temp_real)
                  call InputErrorMsg(input,option,'time increment', &
                                     'OUTPUT,PERIODIC_OBSERVATION,TIME')
                  call InputReadWord(input,option,word,PETSC_TRUE)
                  call InputErrorMsg(input,option,'time increment units', &
                                     'OUTPUT,PERIODIC_OBSERVATION,TIME')
                  units_conversion = UnitsConvertToInternal(word,option) 
                  output_option%periodic_tr_output_time_incr = temp_real* &
                                                               units_conversion
                case('TIMESTEP')
                  call InputReadInt(input,option, &
                                    output_option%periodic_tr_output_ts_imod)
                  call InputErrorMsg(input,option,'timestep increment', &
                                     'OUTPUT,PERIODIC_OBSERVATION,TIMESTEP')
                case default
                  option%io_buffer = 'Keyword: ' // trim(word) // &
                                     ' not recognized in OUTPUT,'// &
                                     'PERIODIC_OBSERVATION,TIMESTEP.'
                  call printErrMsg(option)
              end select
            case('FORMAT')
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'keyword','OUTPUT,FORMAT') 
              call StringToUpper(word)
              select case(trim(word))
                case ('HDF5')
                  output_option%print_hdf5 = PETSC_TRUE
                  call InputReadWord(input,option,word,PETSC_TRUE)
                  call InputDefaultMsg(input,option, &
                                       'OUTPUT,FORMAT,HDF5,# FILES')
                  if (len_trim(word) > 0) then
                    call StringToUpper(word)
                    select case(trim(word))
                      case('SINGLE_FILE')
                        output_option%print_single_h5_file = PETSC_TRUE
                      case('MULTIPLE_FILES')
                        output_option%print_single_h5_file = PETSC_FALSE
                        output_option%times_per_h5_file = 1
                        call InputReadWord(input,option,word,PETSC_TRUE)
                        if (len_trim(word)>0) then
                          select case(trim(word))
                            case('TIMES_PER_FILE')
                              call InputReadInt(input,option, &
                                              output_option%times_per_h5_file)
                              call InputErrorMsg(input,option,'timestep increment', &
                                        'OUTPUT,FORMAT,MULTIPLE_FILES,TIMES_PER_FILE')
                            case default
                              option%io_buffer = 'Keyword: ' // trim(word) // &
                                     ' not recognized in OUTPUT,'// &
                                     'FORMAT,MULTIPLE_FILES,TIMES_PER_FILE.'
                              call printErrMsg(option)
                          end select
                        endif
                      case default
                        option%io_buffer = 'HDF5 keyword (' // trim(word) // &
                          ') not recognized.  Use "SINGLE_FILE" or ' // &
                          '"MULTIPLE_FILES".'
                        call printErrMsg(option)
                    end select
                  endif
                case ('MAD')
                  output_option%print_mad = PETSC_TRUE
                case ('TECPLOT')
                  output_option%print_tecplot = PETSC_TRUE
                  call InputReadWord(input,option,word,PETSC_TRUE)
                  call InputErrorMsg(input,option,'TECPLOT','OUTPUT,FORMAT') 
                  call StringToUpper(word)
                  select case(trim(word))
                    case('POINT')
                      output_option%tecplot_format = TECPLOT_POINT_FORMAT
                    case('BLOCK')
                      output_option%tecplot_format = TECPLOT_BLOCK_FORMAT
                    case('FEBRICK')
                      output_option%tecplot_format = TECPLOT_FEBRICK_FORMAT
                    case default
                      option%io_buffer = 'TECPLOT format (' // trim(word) // &
                                         ') not recongnized.'
                      call printErrMsg(option)
                  end select
                  if (output_option%tecplot_format == TECPLOT_POINT_FORMAT &
                      .and. option%mycommsize > 1) then
                    output_option%tecplot_format = TECPLOT_BLOCK_FORMAT
                  endif
                  if (grid%itype == IMPLICIT_UNSTRUCTURED_GRID) then
                    output_option%tecplot_format = TECPLOT_FEBRICK_FORMAT
                  endif
                case ('VTK')
                  output_option%print_vtk = PETSC_TRUE
                case default
                  option%io_buffer = 'Keyword: ' // trim(word) // &
                                     ' not recognized in OUTPUT,FORMAT.'
                  call printErrMsg(option)
              end select
            case('VELOCITY_AT_CENTER')
              vel_cent = PETSC_TRUE
            case('VELOCITY_AT_FACE')
              vel_face = PETSC_TRUE
            case('FLUXES')
              fluxes = PETSC_TRUE
            case('FLOWRATES','FLOWRATE')
              mass_flowrate = PETSC_TRUE
              energy_flowrate = PETSC_TRUE
            case('MASS_FLOWRATE')
              mass_flowrate = PETSC_TRUE
            case('ENERGY_FLOWRATE')
              energy_flowrate = PETSC_TRUE
            case('AVERAGE_FLOWRATES','AVERAGE_FLOWRATE')
              aveg_mass_flowrate = PETSC_TRUE
              aveg_energy_flowrate = PETSC_TRUE
            case('AVERAGE_MASS_FLOWRATE')
              aveg_mass_flowrate = PETSC_TRUE
            case('AVERAGE_ENERGY_FLOWRATE')
              aveg_energy_flowrate = PETSC_TRUE
            case ('HDF5_WRITE_GROUP_SIZE')
              call InputReadInt(input,option,option%hdf5_write_group_size)
              call InputErrorMsg(input,option,'HDF5_WRITE_GROUP_SIZE','Group size')
            case('VARIABLES')
              call OutputVariableRead(input,option,output_option%output_variable_list)
            case('AVERAGE_VARIABLES')
              call OutputVariableRead(input,option,output_option%aveg_output_variable_list)
            case('VOLUME')
              output_option%print_volume = PETSC_TRUE
            case('ROOT_SYSTEMS')
              output_option%print_root_system = PETSC_TRUE
              call InputReadWord(input,option,word,PETSC_TRUE)
              if (input%ierr == 0) then
                if (StringCompareIgnoreCase(word,'all')) then
                  allocate(output_option%root_system_ids(0:0))
                  output_option%root_system_ids(0) = -1
                else
                  option%io_buffer='Unknow card value for ROOT_SYSTEMS: ' // &
                    trim(word)
                  call printErrMsg(option)
                endif
              else
                size = 100
                allocate(tmp_int(0:size))
                tmp_int = 0
                count = 0
                do
                  call InputReadPflotranString(input,option)
                  call InputReadStringErrorMsg(input,option,card)
                  if (InputCheckExit(input,option)) exit
                  if (tmp_int(0) + 1 > size) then
                    allocate(tmp_int2(0:size))
                    tmp_int2 = tmp_int
                    deallocate(tmp_int)
                    size = size*2
                    allocate(tmp_int(0:size))
                    tmp_int(0:size/2) = tmp_int2(0:size/2)
                  endif
                  tmp_int(0) = tmp_int(0) + 1
                  call InputReadInt(input,option,tmp_int(tmp_int(0)))
                  call InputErrorMsg(input,option,'keyword','ROOT_SYSTEMS')
                enddo
                allocate(output_option%root_system_ids(0:tmp_int(0)))
                output_option%root_system_ids(0:tmp_int(0)) = tmp_int(0:tmp_int(0))
              endif
            case default
              option%io_buffer = 'Keyword: ' // trim(word) // &
                                 ' not recognized in OUTPUT.'
              call printErrMsg(option)              
          end select
        enddo
        if (vel_cent) then
          if (output_option%print_tecplot) &
            output_option%print_tecplot_vel_cent = PETSC_TRUE
          if (output_option%print_hdf5) &
            output_option%print_hdf5_vel_cent = PETSC_TRUE
          if (output_option%print_vtk) &
            output_option%print_vtk_vel_cent = PETSC_TRUE
        endif
        if (vel_face) then
          if (output_option%print_tecplot) &
            output_option%print_tecplot_vel_face = PETSC_TRUE
          if (output_option%print_hdf5) &
           output_option%print_hdf5_vel_face = PETSC_TRUE
        endif
        if (fluxes) then
          output_option%print_fluxes = PETSC_TRUE
        endif
        if(output_option%aveg_output_variable_list%nvars>0) then
          if(output_option%periodic_output_time_incr==0.d0) then
            option%io_buffer = 'Keyword: AVERAGE_VARIABLES defined without' // &
                               ' PERIODIC TIME being set.'
            call printErrMsg(option)
          endif
          if(.not.output_option%print_hdf5) then
            option%io_buffer = 'Keyword: AVERAGE_VARIABLES only defined for FORMAT HDF5'
            call printErrMsg(option)
          endif
        endif
        if (mass_flowrate.or.energy_flowrate.or.aveg_mass_flowrate.or.aveg_energy_flowrate) then
          if (output_option%print_hdf5) then
#ifndef STORE_FLOWRATES
            option%io_buffer='To output FLOWRATES/MASS_FLOWRATE/ENERGY_FLOWRATE, '// &
              'compile with -DSTORE_FLOWRATES'
            call printErrMsg(option)
#endif
            output_option%print_hdf5_mass_flowrate = mass_flowrate
            output_option%print_hdf5_energy_flowrate = energy_flowrate
            output_option%print_hdf5_aveg_mass_flowrate = aveg_mass_flowrate
            output_option%print_hdf5_aveg_energy_flowrate = aveg_energy_flowrate
            if(aveg_mass_flowrate.or.aveg_energy_flowrate) then
              if(output_option%periodic_output_time_incr==0.d0) then
                option%io_buffer = 'Keyword: AVEGRAGE_FLOWRATES/ ' // &
                  'AVEGRAGE_MASS_FLOWRATE/ENERGY_FLOWRATE defined without' // &
                  ' PERIODIC TIME being set.'
                call printErrMsg(option)
              endif
            endif
           option%store_flowrate = PETSC_TRUE
          endif
          if (associated(grid%unstructured_grid%explicit_grid)) then
#ifndef STORE_FLOWRATES          
            option%io_buffer='To output FLOWRATES/MASS_FLOWRATE/ENERGY_FLOWRATE ' // &
              'compile with -DSTORE_FLOWRATES.'
            call printErrMsg(option)
#endif
            output_option%print_explicit_flowrate = mass_flowrate
          endif
        
        endif

!.....................
      case ('REGRESSION')
        call RegressionRead(simulation%regression,input,option)

!.....................
      case ('TIME')
        do
          call InputReadPflotranString(input,option)
          call InputReadStringErrorMsg(input,option,card)
          if (InputCheckExit(input,option)) exit
          call InputReadWord(input,option,word,PETSC_TRUE)
          call InputErrorMsg(input,option,'word','TIME') 
          select case(trim(word))
            case('STEADY_STATE')
              option%steady_state = PETSC_TRUE
            case('FINAL_TIME')
              call InputReadDouble(input,option,temp_real)
              call InputErrorMsg(input,option,'Final Time','TIME') 
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'Final Time Units','TIME')
              realization%output_option%tunit = trim(word)
              realization%output_option%tconv = UnitsConvertToInternal(word,option)
              waypoint => WaypointCreate()
              waypoint%final = PETSC_TRUE
              waypoint%time = temp_real*realization%output_option%tconv
              waypoint%print_output = PETSC_TRUE              
              call WaypointInsertInList(waypoint,realization%waypoint_list)
            case('INITIAL_TIMESTEP_SIZE')
              call InputReadDouble(input,option,temp_real)
              call InputErrorMsg(input,option,'Initial Timestep Size','TIME') 
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'Initial Timestep Size Time Units','TIME')
              default_timestepper%dt_min = temp_real*UnitsConvertToInternal(word,option)
            case('MAXIMUM_TIMESTEP_SIZE')
              call InputReadDouble(input,option,temp_real)
              call InputErrorMsg(input,option,'Maximum Timestep Size','TIME') 
              call InputReadWord(input,option,word,PETSC_TRUE)
              call InputErrorMsg(input,option,'Maximum Timestep Size Time Units','TIME')
              waypoint => WaypointCreate()
              waypoint%dt_max = temp_real*UnitsConvertToInternal(word,option)
              call InputReadWord(input,option,word,PETSC_TRUE)
              if (input%ierr == 0) then
                call StringToUpper(word)
                if (StringCompare(word,'AT',TWO_INTEGER)) then
                  call InputReadDouble(input,option,temp_real)
                  call InputErrorMsg(input,option,'Maximum Timestep Size Update Time','TIME') 
                  call InputReadWord(input,option,word,PETSC_TRUE)
                  call InputErrorMsg(input,option,'Maximum Timestep Size Update Time Units','TIME')
                  waypoint%time = temp_real*UnitsConvertToInternal(word,option)
                else
                  option%io_buffer = 'Keyword under "MAXIMUM_TIMESTEP_SIZE" after ' // &
                                     'maximum timestep size should be "at".'
                  call printErrMsg(option)
                endif
              else
                waypoint%time = 0.d0
              endif     
              call WaypointInsertInList(waypoint,realization%waypoint_list)
            case default
              option%io_buffer = 'Keyword: ' // trim(word) // &
                                 ' not recognized in TIME.'
              call printErrMsg(option)              
          end select
        enddo

        if (associated(flow_timestepper)) then
          flow_timestepper%dt_min = default_timestepper%dt_min
        endif
        if (associated(tran_timestepper)) then
          tran_timestepper%dt_min = default_timestepper%dt_min
        endif
        option%flow_dt = default_timestepper%dt_min
        option%tran_dt = default_timestepper%dt_min
      
!.....................
      case ('SURFACE_FLOW')
        call SurfaceInitReadInput(simulation%surf_realization, &
                              simulation%surf_flow_timestepper%solver,input,option)
        simulation%surf_flow_timestepper%dt_min = simulation%surf_realization%dt_min
        simulation%surf_flow_timestepper%dt_max = simulation%surf_realization%dt_max
        option%surf_subsurf_coupling_flow_dt = simulation%surf_realization%dt_coupling
        option%surf_flow_dt=simulation%surf_flow_timestepper%dt_min

        ! Add first waypoint
        waypoint => WaypointCreate()
        waypoint%time = 0.d0
        call WaypointInsertInList(waypoint,simulation%surf_realization%waypoint_list)

        ! Add final_time waypoint to surface_realization
        waypoint => WaypointCreate()
        waypoint%final = PETSC_TRUE
        waypoint%time = realization%waypoint_list%last%time
        waypoint%print_output = PETSC_TRUE
        call WaypointInsertInList(waypoint,simulation%surf_realization%waypoint_list)

!......................
      case ('GEOMECHANICS')
        call GeomechanicsInitReadInput(geomech_realization, &
                         simulation%geomech_timestepper%solver,input,option)
        ! Add first waypoint
        waypoint => WaypointCreate()
        waypoint%time = 0.d0
        call WaypointInsertInList(waypoint,simulation%geomech_realization%waypoint_list)

        ! Add final_time waypoint to geomech_realization
        waypoint => WaypointCreate()
        waypoint%final = PETSC_TRUE
        waypoint%time = realization%waypoint_list%last%time
        waypoint%print_output = PETSC_TRUE
        call WaypointInsertInList(waypoint,simulation%geomech_realization%waypoint_list)

!......................
      case ('HDF5_READ_GROUP_SIZE')
        call InputReadInt(input,option,option%hdf5_read_group_size)
        call InputErrorMsg(input,option,'HDF5_READ_GROUP_SIZE','Group size')

!......................
      case ('HDF5_WRITE_GROUP_SIZE')
        call InputReadInt(input,option,option%hdf5_write_group_size)
        call InputErrorMsg(input,option,'HDF5_WRITE_GROUP_SIZE','Group size')

!....................
      case ('ROOT_PROPERTY')
        root_property => RootPropertyCreate()
        call InputReadWord(input,option,root_property%name,PETSC_TRUE)
        call InputErrorMsg(input,option,'name','ROOT_PROPERTY')
        call RootPropertyRead(root_property,input,option)
        call RootPropertyAddToList(root_property,realization%root_properties)
        nullify(root_property)

!......................
      case ('ROOT_SYSTEM')
        option%root_system = PETSC_TRUE

!......................
      case ('DBASE_FILENAME')

!....................
      case default
    
        option%io_buffer = 'Keyword ' // trim(word) // ' in input file ' // &
                           'not recognized'
        call printErrMsg(option)

    end select

  enddo
                                      
end subroutine InitReadInput

! ************************************************************************** !

subroutine setFlowMode(option)
  ! 
  ! Sets the flow mode (richards, vadose, mph, etc.)
  ! 
  ! Author: Glenn Hammond
  ! Date: 10/26/07
  ! 

  use Option_module
  use String_module
  use General_Aux_module

  implicit none 

  type(option_type) :: option
  
  call StringToUpper(option%flowmode)
  select case(option%flowmode)
    case('TH')
      option%iflowmode = TH_MODE
      option%nphase = 1
      option%liquid_phase = 1      
      option%gas_phase = 2
      option%nflowdof = 2
      option%nflowspec = 1
      option%use_isothermal = PETSC_FALSE
    case('MIS','MISCIBLE')
      option%iflowmode = MIS_MODE
      option%nphase = 1
      option%liquid_phase = 1      
      option%gas_phase = 2      
      option%nflowdof = 2
      option%nflowspec = 2
      option%io_buffer = 'Material Auxvars must be refactored for MISCIBLE.'
      call printErrMsg(option)
    case('RICHARDS')
      option%iflowmode = RICHARDS_MODE
      option%nphase = 1
      option%liquid_phase = 1      
      option%nflowdof = 1
      option%nflowspec = 1
      option%use_isothermal = PETSC_TRUE
    case('MPH','MPHASE')
      option%iflowmode = MPH_MODE
      option%nphase = 2
      option%liquid_phase = 1      
      option%gas_phase = 2      
      option%nflowdof = 3
      option%nflowspec = 2
      option%itable = 2 ! read CO2DATA0.dat
!     option%itable = 1 ! create CO2 database: co2data.dat
      option%use_isothermal = PETSC_FALSE
    case('FLASH2')
      option%iflowmode = FLASH2_MODE
      option%nphase = 2
      option%liquid_phase = 1      
      option%gas_phase = 2      
      option%nflowdof = 3
      option%nflowspec = 2
      option%itable = 2
      option%use_isothermal = PETSC_FALSE
   case('IMS','IMMIS','THS')
      option%iflowmode = IMS_MODE
      option%nphase = 2
      option%liquid_phase = 1      
      option%gas_phase = 2      
      option%nflowdof = 3
      option%nflowspec = 2
      option%itable = 2
      option%io_buffer = 'Material Auxvars must be refactored for IMMIS.'
      call printErrMsg(option)
    case('GENERAL')
      option%iflowmode = G_MODE
      option%nphase = 2
      option%liquid_phase = 1  ! liquid_pressure
      option%gas_phase = 2     ! gas_pressure

      option%air_pressure_id = 3
      option%capillary_pressure_id = 4
      option%vapor_pressure_id = 5
      option%saturation_pressure_id = 6

      option%water_id = 1
      option%air_id = 2
      option%energy_id = 3

      option%nflowdof = 3
      option%nflowspec = 2
      option%use_isothermal = PETSC_FALSE
    case default
      option%io_buffer = 'Mode: '//trim(option%flowmode)//' not recognized.'
      call printErrMsg(option)
  end select
  
end subroutine setFlowMode

! ************************************************************************** !

subroutine setSurfaceFlowMode(option)
  ! 
  ! Sets the flow mode for surface (richards, th, etc.)
  ! 
  ! Author: Gautam Bisht
  ! Date: 07/30/14
  ! 

  use Option_module
  use String_module

  implicit none 

  type(option_type) :: option
  
  call StringToUpper(option%flowmode)
  select case(option%flowmode)
    case('RICHARDS')
      option%nsurfflowdof = ONE_INTEGER
    case('TH')
      option%nsurfflowdof = TWO_INTEGER
    case default
      option%io_buffer = 'Mode: '//trim(option%flowmode)//' not recognized.'
      call printErrMsg(option)
  end select
  
end subroutine setSurfaceFlowMode

! ************************************************************************** !

subroutine assignVolumesToMaterialAuxVars(realization)
  ! 
  ! Assigns the cell volumes currently stored in field%volume0 to the 
  ! material auxiliary variable object
  ! 
  ! Author: Glenn Hammond
  ! Date: 01/13/14
  ! 

  use Realization_class
  use Option_module
  use Material_module
  use Discretization_module
  use Field_module
  use Variables_module, only : VOLUME
  
  implicit none
  
  type(realization_type) :: realization
  
  type(option_type), pointer :: option
  type(field_type), pointer :: field

  option => realization%option
  field => realization%field

  call DiscretizationGlobalToLocal(realization%discretization,field%volume0, &
                                   field%work_loc,ONEDOF)
  call MaterialSetAuxVarVecLoc(realization%patch%aux%Material, &
                               field%work_loc,VOLUME,ZERO_INTEGER)

end subroutine assignVolumesToMaterialAuxVars

! ************************************************************************** !

subroutine verifyAllCouplers(realization)
  ! 
  ! Verifies the connectivity of a coupler
  ! 
  ! Author: Glenn Hammond
  ! Date: 1/8/08
  ! 

  use Realization_class
  use Patch_module
  use Coupler_module

  implicit none

  type(realization_type) :: realization
  
  type(patch_type), pointer :: cur_patch

  cur_patch => realization%patch_list%first
  do
    if (.not.associated(cur_patch)) exit

      call verifyCoupler(realization,cur_patch,cur_patch%initial_conditions)
      call verifyCoupler(realization,cur_patch,cur_patch%boundary_conditions)
      call verifyCoupler(realization,cur_patch,cur_patch%source_sinks)

    cur_patch => cur_patch%next
  enddo
  
end subroutine verifyAllCouplers

! ************************************************************************** !

subroutine verifyCoupler(realization,patch,coupler_list)
  ! 
  ! Verifies the connectivity of a coupler
  ! 
  ! Author: Glenn Hammond
  ! Date: 1/8/08
  ! 

  use Realization_class
  use Discretization_module
  use Option_module 
  use Coupler_module
  use Condition_module
  use Grid_module
  use Output_module
  use Output_Tecplot_module, only : OutputVectorTecplot  
  use Patch_module

  implicit none

  type(realization_type) :: realization
  type(coupler_list_type), pointer :: coupler_list

  type(option_type), pointer :: option
  type(grid_type), pointer :: grid
  type(patch_type), pointer :: patch  
  type(coupler_type), pointer :: coupler
  character(len=MAXWORDLENGTH) :: word
  character(len=MAXSTRINGLENGTH) :: filename
  character(len=MAXSTRINGLENGTH) :: dataset_name
  PetscInt :: iconn, icell, local_id
  Vec :: global_vec
  PetscReal, pointer :: vec_ptr(:)
  PetscErrorCode :: ierr

  patch => realization%patch
  grid => patch%grid
  option => realization%option

  if (.not.associated(coupler_list)) return

  call DiscretizationCreateVector(realization%discretization,ONEDOF, &
                                  global_vec,GLOBAL,option)

  coupler => coupler_list%first

  do
    if (.not.associated(coupler)) exit

    call VecZeroEntries(global_vec,ierr);CHKERRQ(ierr)
    call VecGetArrayF90(global_vec,vec_ptr,ierr);CHKERRQ(ierr)
    if (associated(coupler%connection_set)) then
      do iconn = 1, coupler%connection_set%num_connections
        local_id = coupler%connection_set%id_dn(iconn)
!        vec_ptr(local_id) = coupler%id
!geh: let's sum the # of connections
         vec_ptr(local_id) = vec_ptr(local_id) + 1
      enddo
    else
      if (associated(coupler%region)) then
        do icell = 1, coupler%region%num_cells
          local_id = coupler%region%cell_ids(icell)
!          vec_ptr(local_id) = coupler%id
         vec_ptr(local_id) = vec_ptr(local_id) + 1
        enddo
      endif
    endif
    call VecRestoreArrayF90(global_vec,vec_ptr,ierr);CHKERRQ(ierr)
    if (len_trim(coupler%flow_condition_name) > 0) then
      dataset_name = coupler%flow_condition_name
    else if (len_trim(coupler%tran_condition_name) > 0) then
      dataset_name = coupler%tran_condition_name
    endif
    write(word,*) patch%id
    dataset_name = trim(dataset_name) // '_' // &
                   trim(coupler%region%name) // '_' // &
                   trim(adjustl(word))
    dataset_name = dataset_name(1:28)
    filename = trim(dataset_name) // '.tec'
    call OutputVectorTecplot(filename,dataset_name,realization,global_vec)

    coupler => coupler%next
  enddo

  call VecDestroy(global_vec,ierr);CHKERRQ(ierr)

end subroutine verifyCoupler

! ************************************************************************** !

subroutine readRegionFiles(realization)
  ! 
  ! Reads in grid cell ids stored in files
  ! 
  ! Author: Glenn Hammond
  ! Date: 1/03/08
  ! 

  use Realization_class
  use Region_module
  use HDF5_module

  implicit none

  type(realization_type) :: realization
  
  type(region_type), pointer :: region
 
  
  region => realization%regions%first
  do 
    if (.not.associated(region)) exit
    if (len_trim(region%filename) > 1) then
      if (index(region%filename,'.h5') > 0) then
        if (region%grid_type == STRUCTURED_GRID_REGION) then
          call HDF5ReadRegionFromFile(realization,region,region%filename)
        else
          !geh: Do not skip this subouroutine if PETSC_HAVE_HDF5 is not 
          !     defined.  The subroutine prints an error message if not defined
          !     informing the user of the error.  If you skip the subroutine,
          !     no error message is printed and the user is unaware of the
          !     region not being read.
          call HDF5ReadUnstructuredGridRegionFromFile(realization%option,region, &
                                                      region%filename)
        endif
      else if (index(region%filename,'.ss') > 0) then
        region%def_type = DEFINED_BY_SIDESET_UGRID
        region%sideset => RegionCreateSideset()
        call RegionReadFromFile(region%sideset,region%filename, &
                                realization%option)
      else if (index(region%filename,'.ex') > 0) then
        region%def_type = DEFINED_BY_FACE_UGRID_EXP
        call RegionReadFromFile(region%explicit_faceset,region%cell_ids, &
                                region%filename,realization%option)
        region%num_cells = size(region%cell_ids)
      else
        call RegionReadFromFile(region,realization%option, &
                                region%filename)
      endif
    endif
    region => region%next
  enddo

end subroutine readRegionFiles

! ************************************************************************** !

subroutine readVectorFromFile(realization,vector,filename,vector_type)
  ! 
  ! Reads data from a file into an associated vector
  ! 
  ! Author: Glenn Hammond
  ! Date: 03/18/08
  ! 

  use Realization_class
  use Discretization_module
  use Field_module
  use Grid_module
  use Option_module
  use Patch_module
  use Logging_module

  use HDF5_module
  
  implicit none
  
  type(realization_type) :: realization
  Vec :: vector
  character(len=MAXWORDLENGTH) :: filename
  PetscInt :: vector_type
  
  type(discretization_type), pointer :: discretization
  type(field_type), pointer :: field
  type(grid_type), pointer :: grid
  type(option_type), pointer :: option
  type(patch_type), pointer :: patch   
  PetscInt :: ghosted_id, natural_id, material_id
  PetscInt :: fid = 86
  PetscInt :: status
  PetscErrorCode :: ierr
  PetscInt :: count, read_count, i
  PetscInt :: flag
  PetscInt, pointer :: indices(:)
  PetscReal, pointer :: values(:)
  PetscInt, parameter :: block_size = 10000
  Vec :: natural_vec, global_vec

  discretization => realization%discretization
  field => realization%field
  patch => realization%patch
  grid => patch%grid
  option => realization%option

  if (index(filename,'.h5') > 0) then
    ! to be taken care of later in readPermeabilitiesFromFile()
  else
    open(unit=fid,file=filename,status="old",iostat=status)
    if (status /= 0) then
      option%io_buffer = 'File: ' // trim(filename) // ' not found.'
      call printErrMsg(option)
    endif
    allocate(values(block_size))
    allocate(indices(block_size))
    call DiscretizationCreateVector(discretization,ONEDOF,natural_vec, &
                                    NATURAL,option)
    count = 0
    do
      if (count >= grid%nmax) exit
      read_count = min(block_size,grid%nmax-count)
      do i=1,read_count
        indices(i) = count+i-1 ! zero-based indexing
      enddo
      ierr = 0
      if (option%myrank == option%io_rank) &
        read(fid,*,iostat=ierr) values(1:read_count)
      flag = ierr
      call MPI_Bcast(flag,ONE_INTEGER_MPI,MPIU_INTEGER,option%io_rank, &
                     option%mycomm,ierr)      
      if (flag /= 0) then
        option%io_buffer = 'Insufficent data in file: ' // filename
        call printErrMsg(option)
      endif
      if (option%myrank == option%io_rank) then
        call VecSetValues(natural_vec,read_count,indices,values,INSERT_VALUES, &
                          ierr);CHKERRQ(ierr)
      endif
      count = count + read_count
    enddo
    call MPI_Bcast(count,ONE_INTEGER_MPI,MPIU_INTEGER,option%io_rank, &
                   option%mycomm,ierr)      
    if (count /= grid%nmax) then
      write(option%io_buffer,'("Number of data in file (",i8, &
      & ") does not match size of vector (",i8,")")') count, grid%nlmax
      call printErrMsg(option)
    endif
    close(fid)
    deallocate(values)
    nullify(values)
    deallocate(indices)
    nullify(indices)
    call VecAssemblyBegin(natural_vec,ierr);CHKERRQ(ierr)
    call VecAssemblyEnd(natural_vec,ierr);CHKERRQ(ierr)
    select case(vector_type)
      case(LOCAL)
        call DiscretizationCreateVector(discretization,ONEDOF,global_vec, &
                                        GLOBAL,option)        
        call DiscretizationNaturalToGlobal(discretization,natural_vec, &
                                           global_vec,ONEDOF)  
        call DiscretizationGlobalToLocal(discretization,global_vec, &
                                         vector,ONEDOF)
        call VecDestroy(global_vec,ierr);CHKERRQ(ierr)
      case(GLOBAL)
        call DiscretizationNaturalToGlobal(discretization,natural_vec, &
                                           vector,ONEDOF) 
    end select 
    call VecDestroy(natural_vec,ierr);CHKERRQ(ierr)
  endif
  
end subroutine readVectorFromFile

! ************************************************************************** !

subroutine readFlowInitialCondition(realization,filename)
  ! 
  ! Assigns flow initial condition from HDF5 file
  ! 
  ! Author: Glenn Hammond
  ! Date: 03/05/10
  ! 

  use Realization_class
  use Option_module
  use Field_module
  use Grid_module
  use Patch_module
  use Discretization_module
  use HDF5_module
  
  implicit none

#include "finclude/petscvec.h"
#include "finclude/petscvec.h90"
  
  type(realization_type) :: realization
  character(len=MAXSTRINGLENGTH) :: filename
  
  PetscInt :: local_id, idx, offset
  PetscReal, pointer :: xx_p(:)
  character(len=MAXSTRINGLENGTH) :: group_name
  character(len=MAXSTRINGLENGTH) :: dataset_name
  PetscReal, pointer :: vec_p(:)  
  PetscErrorCode :: ierr
  
  type(option_type), pointer :: option
  type(field_type), pointer :: field  
  type(patch_type), pointer :: patch
  type(grid_type), pointer :: grid
  type(discretization_type), pointer :: discretization
  type(patch_type), pointer :: cur_patch

  option => realization%option
  discretization => realization%discretization
  field => realization%field
  patch => realization%patch

  if (option%iflowmode /= RICHARDS_MODE) then
    option%io_buffer = 'Reading of flow initial conditions from HDF5 ' // &
                       'file (' // trim(filename) // &
                       'not currently not supported for mode: ' // &

                       trim(option%flowmode)
  endif      

  cur_patch => realization%patch_list%first
  do
    if (.not.associated(cur_patch)) exit

    grid => cur_patch%grid

      ! assign initial conditions values to domain
    call VecGetArrayF90(field%flow_xx, xx_p, ierr);CHKERRQ(ierr)

    ! Pressure for all modes 
    offset = 1
    group_name = ''
    dataset_name = 'Pressure'
    call HDF5ReadCellIndexedRealArray(realization,field%work, &
                                      filename,group_name, &
                                      dataset_name,option%id>0)
    call VecGetArrayF90(field%work,vec_p,ierr);CHKERRQ(ierr)
    do local_id=1, grid%nlmax
      if (cur_patch%imat(grid%nL2G(local_id)) <= 0) cycle
      if (dabs(vec_p(local_id)) < 1.d-40) then
        print *,  option%myrank, grid%nG2A(grid%nL2G(local_id)), &
              ': Potential error - zero pressure in Initial Condition read from file.'
      endif
      idx = (local_id-1)*option%nflowdof + offset
      xx_p(idx) = vec_p(local_id)
    enddo
    call VecRestoreArrayF90(field%work,vec_p,ierr);CHKERRQ(ierr)

    call VecRestoreArrayF90(field%flow_xx,xx_p, ierr);CHKERRQ(ierr)
        
    cur_patch => cur_patch%next
  enddo
   
  ! update dependent vectors
  call DiscretizationGlobalToLocal(discretization,field%flow_xx, &
                                   field%flow_xx_loc,NFLOWDOF)  
  call VecCopy(field%flow_xx, field%flow_yy, ierr);CHKERRQ(ierr)

end subroutine readFlowInitialCondition

! ************************************************************************** !

subroutine Create_IOGroups(option)
  ! 
  ! Create sub-communicators that are used in initialization
  ! and output HDF5 routines.
  ! 
  ! Author: Vamsi Sripathi
  ! Date: 07/14/09
  ! 

  use Option_module
  use Logging_module

#if defined(SCORPIO)
  use hdf5
#endif

  implicit none

  type(option_type) :: option
  PetscErrorCode :: ierr

#if defined(SCORPIO)

  PetscMPIInt :: numiogroups

  call PetscLogEventBegin(logging%event_create_iogroups,ierr);CHKERRQ(ierr)

  ! Initialize HDF interface to define global constants  
  call h5open_f(ierr)

  if (option%hdf5_read_group_size <= 0) then
    write(option%io_buffer,& 
          '("The keyword HDF5_READ_GROUP_SIZE & 
            & in the input file (pflotran.in) is either not set or &
            & its value is less than or equal to ZERO. &
            & HDF5_READ_GROUP_SIZE =  ",i6)') &
             option%hdf5_read_group_size
    !call printErrMsg(option)
    call printMsg(option)
    ! default is to let one process read and broadcast to everyone
    option%hdf5_read_group_size = option%mycommsize
  endif         
 
  if (option%hdf5_write_group_size <= 0) then
    write(option%io_buffer,& 
          '("The keyword HDF5_WRITE_GROUP_SIZE & 
            &in the input file (pflotran.in) is either not set or &
            &its value is less than or equal to ZERO. &
            &HDF5_WRITE_GROUP_SIZE =  ",i6)') &
             option%hdf5_write_group_size
    !call printErrMsg(option)
    call printMsg(option)
    ! default is to let everyone write separately 
    option%hdf5_write_group_size = 1
  endif                    

  ! create read IO groups
  numiogroups = option%mycommsize/option%hdf5_read_group_size
  call scorpio_iogroup_init(numiogroups, option%mycomm, option%ioread_group_id, ierr)

  if ( option%hdf5_read_group_size == option%hdf5_write_group_size ) then
    ! reuse read_group to use for writing too as both groups are same size
    option%iowrite_group_id = option%ioread_group_id
  else   
      ! create write IO groups
      numiogroups = option%mycommsize/option%hdf5_write_group_size
      call scorpio_iogroup_init(numiogroups, option%mycomm, option%iowrite_group_id, ierr)
  end if

    write(option%io_buffer, '(" Read group id :  ", i6)') option%ioread_group_id
    call printMsg(option)      
    write(option%io_buffer, '(" Write group id :  ", i6)') option%iowrite_group_id
    call printMsg(option)      
  call PetscLogEventEnd(logging%event_create_iogroups,ierr);CHKERRQ(ierr)
#endif
! SCORPIO
 
end subroutine Create_IOGroups

! ************************************************************************** !

subroutine InitPrintPFLOTRANHeader(option,fid)
  ! 
  ! Initializes pflotran
  ! 
  ! Author: Glenn Hammond
  ! Date: 10/23/07
  ! 

  use Option_module
  
  implicit none
  
  PetscInt :: fid
  
  type(option_type) :: option
  
  write(fid,'(" PFLOTRAN Header")') 
  
end subroutine InitPrintPFLOTRANHeader

! ************************************************************************** !

subroutine InitReadVelocityField(realization)
  ! 
  ! Reads fluxes in for transport with no flow.
  ! 
  ! Author: Glenn Hammond
  ! Date: 02/05/13
  ! 

  use Realization_class
  use Patch_module
  use Field_module
  use Grid_module
  use Option_module
  use Coupler_module
  use Connection_module
  use Discretization_module
  use HDF5_module

  implicit none
  
  type(realization_type) :: realization
  character(len=MAXSTRINGLENGTH) :: filename
  
  type(field_type), pointer :: field
  type(patch_type), pointer :: patch
  type(grid_type), pointer :: grid
  type(discretization_type), pointer :: discretization
  type(option_type), pointer :: option
  character(len=MAXSTRINGLENGTH) :: group_name
  character(len=MAXSTRINGLENGTH) :: dataset_name
  PetscInt :: idir, iconn, sum_connection
  PetscInt :: ghosted_id_up, local_id
  PetscErrorCode :: ierr
  
  PetscReal, pointer :: vec_loc_p(:)
  PetscReal, pointer :: vec_p(:)
  type(coupler_type), pointer :: boundary_condition  
  type(connection_set_list_type), pointer :: connection_set_list
  type(connection_set_type), pointer :: cur_connection_set
  
  field => realization%field
  patch => realization%patch
  grid => patch%grid
  option => realization%option
  discretization => realization%discretization
  
  filename = realization%nonuniform_velocity_filename

  group_name = ''
  do idir = 1, 3
    select case(idir)
      case(1)
        dataset_name = 'Internal Velocity X'
      case(2)
        dataset_name = 'Internal Velocity Y'
      case(3)
        dataset_name = 'Internal Velocity Z'
    end select
    call HDF5ReadCellIndexedRealArray(realization,field%work,filename, &
                                      group_name,dataset_name,PETSC_FALSE)
    call DiscretizationGlobalToLocal(discretization,field%work,field%work_loc, &
                                     ONEDOF)
    call VecGetArrayF90(field%work_loc,vec_loc_p,ierr);CHKERRQ(ierr)
    connection_set_list => grid%internal_connection_set_list
    cur_connection_set => connection_set_list%first
    sum_connection = 0  
    do 
      if (.not.associated(cur_connection_set)) exit
      do iconn = 1, cur_connection_set%num_connections
        sum_connection = sum_connection + 1
        ghosted_id_up = cur_connection_set%id_up(iconn)
        if (cur_connection_set%dist(idir,iconn) > 0.9d0) then
          patch%internal_velocities(1,sum_connection) = vec_loc_p(ghosted_id_up)
        endif
      enddo
      cur_connection_set => cur_connection_set%next
    enddo
    call VecRestoreArrayF90(field%work_loc,vec_loc_p,ierr);CHKERRQ(ierr)
  enddo
  
  boundary_condition => patch%boundary_conditions%first
  sum_connection = 0    
  do 
    if (.not.associated(boundary_condition)) exit
    dataset_name = boundary_condition%name
    call HDF5ReadCellIndexedRealArray(realization,field%work,filename, &
                                      group_name,dataset_name,PETSC_FALSE)
    call VecGetArrayF90(field%work,vec_p,ierr);CHKERRQ(ierr)
    cur_connection_set => boundary_condition%connection_set
    do iconn = 1, cur_connection_set%num_connections
      sum_connection = sum_connection + 1
      local_id = cur_connection_set%id_dn(iconn)
      patch%boundary_velocities(1,sum_connection) = vec_p(local_id)
    enddo
    call VecRestoreArrayF90(field%work,vec_p,ierr);CHKERRQ(ierr)
    boundary_condition => boundary_condition%next
  enddo
  
end subroutine InitReadVelocityField

! ************************************************************************** !

subroutine SandboxesSetup(realization)
  ! 
  ! Initializes sandbox objects.
  ! 
  ! Author: Glenn Hammond
  ! Date: 05/06/14

  use Realization_class
  use SrcSink_Sandbox_module
  
  type(realization_type) :: realization
  
   call SSSandboxSetup(realization%patch%regions,realization%option)
  
end subroutine SandboxesSetup

! ************************************************************************** !
!> This routine assinges root properties to associated regions in the model
!!
!> @author
!! Gautam Bisht, LBNL
!!
!! date: 09/21/13
! ************************************************************************** !
subroutine assignRootPropToRegions(realization)

  use Realization_class
  use Discretization_module
  use Strata_module
  use Region_module
  use Root_module
  use Option_module
  use Grid_module
  use Field_module
  use Patch_module

  use HDF5_module

  implicit none

  type(realization_type) :: realization
  
  PetscReal, pointer :: kcomp_loc_p(:)
  PetscReal, pointer :: ssd_loc_p(:)
  PetscReal, pointer :: hthreshold_loc_p(:)
  PetscReal, pointer :: vec_p(:)

  PetscInt :: icell, local_id, ghosted_id, natural_id, root_id, iroot
  PetscInt :: istart, iend
  character(len=MAXSTRINGLENGTH) :: group_name
  character(len=MAXSTRINGLENGTH) :: dataset_name
  PetscErrorCode :: ierr
  
  type(option_type), pointer :: option
  type(grid_type), pointer :: grid
  type(discretization_type), pointer :: discretization
  type(field_type), pointer :: field
  type(strata_type), pointer :: strata
  type(patch_type), pointer :: patch  
  type(patch_type), pointer :: cur_patch

  type(root_property_type), pointer :: root_property, null_root_property
  type(region_type), pointer :: region
  PetscBool :: update_ghosted_root_ids
  
  option => realization%option
  discretization => realization%discretization
  patch => realization%patch
  field => realization%field

  option%nglb_root_sys = size(realization%root_property_array)

  cur_patch => realization%patch_list%first
  do
    if (.not.associated(cur_patch)) exit
    if (.not.associated(cur_patch%iroot)) then
      allocate(cur_patch%iroot(0:option%nglb_root_sys,cur_patch%grid%ngmax))
      ! initialize to "unset"
      cur_patch%iroot(:,:) = -999
      cur_patch%iroot(0,:) = 0
    endif
    cur_patch => cur_patch%next
  enddo

  ! if root ids are set based on region, as opposed to being read in
  ! we must communicate the ghosted ids.  This flag toggles this operation.
  update_ghosted_root_ids = PETSC_FALSE
  cur_patch => realization%patch_list%first
  do
    if (.not.associated(cur_patch)) exit
    grid => cur_patch%grid
    strata => cur_patch%strata%first
    do
      if (.not.associated(strata)) exit
      ! Read in cell by cell root ids if they exist
      if (.not.associated(strata%region) .and. strata%active) then
        !call readRootsFromFile(realization,strata%realization_dependent, &
        !                           strata%root_property_filename)
        call printErrMsg(option,'Add readRootsFromFile')
      ! Otherwise, set based on region
      else
        update_ghosted_root_ids = PETSC_TRUE
        region => strata%region
        root_property => strata%root_property
        if (associated(region)) then
          istart = 1
          iend = region%num_cells
        else
          istart = 1
          iend = grid%nlmax
        endif
        do icell=istart, iend
          if (associated(region)) then
            local_id = region%cell_ids(icell)
          else
            local_id = icell
          endif
          ghosted_id = grid%nL2G(local_id)
          if (strata%active) then
            cur_patch%iroot(0,ghosted_id) = cur_patch%iroot(0,ghosted_id) + 1
            cur_patch%iroot(cur_patch%iroot(0,ghosted_id),ghosted_id) = root_property%id
          else
            ! if not active, set root id to zero
            cur_patch%iroot(:,ghosted_id) = 0
          endif
        enddo
      endif
      strata => strata%next
    enddo
    cur_patch => cur_patch%next
  enddo

  ! Allocate memory for ssd_loc
  call VecCreate(option%mycomm, field%ssd_loc, ierr)
  call VecSetSizes(field%ssd_loc, grid%ngmax*option%nglb_root_sys, &
                  PETSC_DECIDE, ierr)
  call VecSetBlockSize(field%ssd_loc, option%nglb_root_sys, ierr)
  call VecSetFromOptions(field%ssd_loc, ierr)

  ! set cell by cell root properties
  ! create null root property for inactive cells
  null_root_property => RootPropertyCreate()
  cur_patch => realization%patch_list%first
  do
    if (.not.associated(cur_patch)) exit
    grid => cur_patch%grid
    call VecGetArrayF90(field%ssd_loc,ssd_loc_p,ierr)
    call VecGetArrayF90(field%kcomp_loc,kcomp_loc_p,ierr)
    call VecGetArrayF90(field%hthreshold_loc,hthreshold_loc_p,ierr)

    do local_id = 1, grid%nlmax
      ghosted_id = grid%nL2G(local_id)
      do iroot = 1,cur_patch%iroot(0,ghosted_id)
        root_id = cur_patch%iroot(iroot, ghosted_id)
        if (root_id == 0) then ! accommodate inactive cells
          root_property => null_root_property
        else if (root_id > 0 .and. &
                 root_id <= &
                 size(realization%root_property_array)) then
          root_property => &
            realization%root_property_array(root_id)%ptr
          if (.not.associated(root_property)) then
            write(dataset_name,*) root_id
            option%io_buffer = 'No root property for root id ' // &
                               trim(adjustl(dataset_name)) &
                               //  ' defined in input file.'
            call printErrMsgByRank(option)
          endif
        else if (root_id < -998) then 
          write(dataset_name,*) grid%nG2A(ghosted_id)
          option%io_buffer = 'Uninitialized root id in patch at cell ' // &
                             trim(adjustl(dataset_name))
          call printErrMsgByRank(option)
        else if (root_id > size(realization%root_property_array)) then
          write(option%io_buffer,*) root_id
          option%io_buffer = 'Unmatched root id in patch:' // &
            adjustl(trim(option%io_buffer))
          call printErrMsgByRank(option)
        else
          option%io_buffer = 'Something messed up with root ids. ' // &
            ' Possibly root ids not assigned to all grid cells. ' // &
            ' Contact Gautam!'
          call printErrMsgByRank(option)
        endif
        kcomp_loc_p(ghosted_id) = root_property%Kcomp
        hthreshold_loc_p(ghosted_id) = root_property%Hthreshold

        ssd_loc_p((ghosted_id-1)*option%nglb_root_sys + root_property%id) = &
          root_property%ssd
      enddo
    enddo

    call VecRestoreArrayF90(field%ssd_loc,ssd_loc_p,ierr)
    call VecRestoreArrayF90(field%kcomp_loc,kcomp_loc_p,ierr)
    call VecRestoreArrayF90(field%hthreshold_loc,hthreshold_loc_p,ierr)

    ! read in any user-defined property fields
    do root_id = 1, size(realization%root_property_array)
      root_property => &
             realization%root_property_array(root_id)%ptr
      if (associated(root_property)) then
        if (associated(root_property%ssd_dataset)) then
          call readSSDFromFile(realization,root_property)
        endif
      endif
    enddo

    cur_patch => cur_patch%next
  enddo
  call RootPropertyDestroy(null_root_property)
  nullify(null_root_property)

  ! update ghosted values
  call DiscretizationLocalToLocal(discretization,field%kcomp_loc, &
                                   field%kcomp_loc,ONEDOF)
  call DiscretizationLocalToLocal(discretization,field%hthreshold_loc, &
                                   field%hthreshold_loc,ONEDOF)


  ! Create the PETSc vector to save transpiration sink at root-system level
  ! for output
  call VecCreate(option%mycomm, field%trootsys_loc, ierr)
  call VecSetSizes(field%trootsys_loc, grid%ngmax*option%nglb_root_sys, &
                  PETSC_DECIDE, ierr)
  call VecSetBlockSize(field%trootsys_loc, option%nglb_root_sys, ierr)
  call VecSetFromOptions(field%trootsys_loc, ierr)

end subroutine assignRootPropToRegions

! ************************************************************************** !
subroutine readSSDFromFile(realization,root_property)
  ! 
  ! Reads SSD data from HDF5 for non-overlapping root system.
  ! 
  ! Author: Gautam Bisht
  ! Date: 08/09/14

#if defined(PETSC_HAVE_HDF5)
  use hdf5
#endif

! 64-bit stuff
#ifdef PETSC_USE_64BIT_INDICES
#define HDF_NATIVE_INTEGER H5T_NATIVE_INTEGER
#else
#define HDF_NATIVE_INTEGER H5T_NATIVE_INTEGER
#endif

  use Realization_class
  use Field_module
  use Grid_module
  use Option_module
  use Patch_module
  use Discretization_module
  use Logging_module
  use Input_Aux_module
  use Root_module
  use HDF5_module
  
  implicit none
  
  type(realization_type) :: realization
  type(root_property_type) :: root_property

  type(field_type), pointer :: field
  type(patch_type), pointer :: patch
  type(grid_type), pointer :: grid
  type(option_type), pointer :: option
  type(input_type), pointer :: input
  type(discretization_type), pointer :: discretization
  character(len=MAXSTRINGLENGTH) :: group_name
  character(len=MAXSTRINGLENGTH) :: dataset_name
  character(len=MAXSTRINGLENGTH) :: string 
  PetscInt :: local_id, ghosted_id, natural_id
  PetscReal :: permeability
  PetscBool :: append_realization_id
  PetscInt :: fid = 86
  PetscInt :: status
  PetscInt :: idirection
  PetscInt :: temp_int
  PetscReal :: ratio, scale
  Vec :: natural_vec
  PetscErrorCode :: ierr
  PetscInt :: num_root_prop
  type(root_property_type), pointer :: cur_root_property

  PetscReal, pointer :: vec_p(:)
  PetscReal, pointer :: ssd_loc_p(:)
  PetscReal, pointer :: real_buffer(:)
  PetscInt, pointer :: int_buffer(:)
  PetscInt  :: istart, iend, ii, jj
  PetscInt :: num_cells_local, num_cells_global
  PetscInt :: remainder
  PetscInt, pointer :: indices(:)
  PetscReal, pointer :: nat_vec_p(:)
  PetscReal, pointer :: glb_vec_p(:)
  PetscInt :: rr

  PetscMPIInt       :: hdf5_err
  PetscMPIInt       :: rank_mpi
  PetscInt          :: ndims
#if defined(PETSC_HAVE_HDF5)
  integer(HID_T) :: file_id
  integer(HID_T) :: grp_id
  integer(HID_T) :: prop_id
  integer(HID_T) :: data_set_id
  integer(HID_T) :: file_space_id
  integer(HID_T) :: data_space_id
  integer(HID_T) :: memory_space_id
  integer(HSIZE_T), allocatable :: dims_h5(:), max_dims_h5(:)
  integer(HSIZE_T) :: offset(1), length(1), stride(1), block(1), dims(1)
#endif

  field => realization%field
  patch => realization%patch
  grid => patch%grid
  option => realization%option
  discretization => realization%discretization

  ! Initialize FORTRAN predefined datatypes
  call h5open_f(hdf5_err)

  ! Setup file access property with parallel I/O access
  call h5pcreate_f(H5P_FILE_ACCESS_F, prop_id, hdf5_err)

#ifndef SERIAL_HDF5
  call h5pset_fapl_mpio_f(prop_id,option%mycomm, MPI_INFO_NULL, hdf5_err)
#endif

  ! Open the file collectively
  call h5fopen_f(root_property%ssd_dataset%filename, H5F_ACC_RDONLY_F, &
                 file_id, hdf5_err, prop_id)
  call h5pclose_f(prop_id, hdf5_err)
  
  !
  ! Read "StandardWaterUpdateDensity" dataset
  !

  ! Open dataset
  call h5dopen_f(file_id, "StandardWaterUpdateDensity", data_set_id, hdf5_err)

  ! Get dataset's dataspace
  call h5dget_space_f(data_set_id, data_space_id, hdf5_err)

  ! Get number of dimensions and check
  call h5sget_simple_extent_ndims_f(data_space_id, ndims, hdf5_err)
  if (ndims > 1) &
    call printErrMsg(option,'Dimension of StandardWaterUpdateDensity in ' // &
                     'HDF5 exceeds 1')

  allocate(dims_h5(ndims))
  allocate(max_dims_h5(ndims))
  
  ! Get dimensions of dataset
  call h5sget_simple_extent_dims_f(data_space_id, dims_h5, max_dims_h5, &
                                   hdf5_err)

  num_cells_global = INT(dims_h5(1))
  num_cells_local  = num_cells_global/option%mycommsize
  remainder = num_cells_global - num_cells_local*option%mycommsize
  if (option%myrank < remainder) num_cells_local = num_cells_local + 1

  ! Find istart and iend for hyperslab
  istart = 0
  iend = 0
  call MPI_Exscan(num_cells_local, istart, ONE_INTEGER_MPI, MPIU_INTEGER, &
                  MPI_SUM, option%mycomm, ierr)
  call MPI_Scan(num_cells_local, iend, ONE_INTEGER_MPI, MPIU_INTEGER, &
                MPI_SUM, option%mycomm, ierr)

  ! Determine the length and offset of data to be read by each processor
  length(1) = iend-istart
  offset(1) = istart
  
  ! initialize dataset rank
  rank_mpi = 1
  memory_space_id = -1

  ! Create data space for dataset
  call h5screate_simple_f(rank_mpi, length, memory_space_id, hdf5_err)
  
  ! Select hyperslab
  call h5dget_space_f(data_set_id, data_space_id, hdf5_err)
  call h5sselect_hyperslab_f(data_space_id, H5S_SELECT_SET_F, offset, length, &
                             hdf5_err)

  ! Initialize data buffer
  allocate(real_buffer(length(1)))
  allocate(int_buffer(length(1)))

  ! Create property list
  call h5pcreate_f(H5P_DATASET_XFER_F, prop_id, hdf5_err)
#ifndef SERIAL_HDF5
  call h5pset_dxpl_mpio_f(prop_id, H5FD_MPIO_COLLECTIVE_F, hdf5_err)
#endif
  
  ! Read the dataset collectively
  call h5dread_f(data_set_id, H5T_NATIVE_DOUBLE, real_buffer, &
                 dims_h5, hdf5_err, memory_space_id, data_space_id)

  call h5sclose_f(data_space_id,hdf5_err)
  call h5dclose_f(data_set_id, hdf5_err)

  !
  ! Read "Cell Ids" dataset
  !

  ! Open dataset
  call h5dopen_f(file_id, "Cell Ids", data_set_id, hdf5_err)

  ! Get dataset's dataspace
  call h5dget_space_f(data_set_id, data_space_id, hdf5_err)

  ! initialize dataset rank
  rank_mpi = 1
  memory_space_id = -1

  ! Create data space for dataset
  call h5screate_simple_f(rank_mpi, length, memory_space_id, hdf5_err)
  
  ! Select hyperslab
  call h5dget_space_f(data_set_id, data_space_id, hdf5_err)
  call h5sselect_hyperslab_f(data_space_id, H5S_SELECT_SET_F, offset, length, &
                             hdf5_err)

  ! Create property list
  call h5pcreate_f(H5P_DATASET_XFER_F, prop_id, hdf5_err)
#ifndef SERIAL_HDF5
  call h5pset_dxpl_mpio_f(prop_id, H5FD_MPIO_COLLECTIVE_F, hdf5_err)
#endif
  
  ! Read the dataset collectively
  call h5dread_f(data_set_id, H5T_NATIVE_INTEGER, int_buffer, &
                 dims_h5, hdf5_err, memory_space_id, data_space_id)

  ! Close
  call h5sclose_f(data_space_id,hdf5_err)
  call h5dclose_f(data_set_id, hdf5_err)
  call h5fclose_f(file_id, hdf5_err)
  call h5close_f(hdf5_err)

  call DiscretizationCreateVector(discretization, ONEDOF, natural_vec, &
                                  NATURAL, option)

  ! Converting 1-based index to 0-based
  int_buffer(:) = int_buffer(:) - 1

  call VecSetValues(natural_vec, length(1), int_buffer, real_buffer, INSERT_VALUES, ierr)
  call VecAssemblyBegin(natural_vec, ierr)
  call VecAssemblyEnd(natural_vec, ierr)

  call VecZeroEntries(field%work_loc,ierr);CHKERRQ(ierr)
  call VecZeroEntries(field%work,ierr);CHKERRQ(ierr)

  call DiscretizationNaturalToGlobal(discretization, natural_vec, &
                                     field%work, ONEDOF)
  call DiscretizationGlobalToLocal(discretization, field%work, &
                                   field%work_loc, ONEDOF)

  call VecGetArrayF90(field%work_loc,vec_p,ierr)
  call VecGetArrayF90(field%ssd_loc,ssd_loc_p,ierr)

  do ghosted_id = 1, grid%ngmax
    local_id = grid%nG2L(ghosted_id)
    if (vec_p(ghosted_id) > 0.d0) then
      if (local_id < 1) then
        patch%iroot(0,ghosted_id) = patch%iroot(0,ghosted_id) + 1
        patch%iroot(patch%iroot(0,ghosted_id),ghosted_id) = root_property%id
      endif
      ssd_loc_p((ghosted_id-1)*option%nglb_root_sys + root_property%id) = &
          vec_p(ghosted_id)
    endif
    !endif
  enddo
  call VecRestoreArrayF90(field%ssd_loc,ssd_loc_p,ierr)
  call VecRestoreArrayF90(field%work_loc,vec_p,ierr)

  call VecDestroy(natural_vec,ierr);CHKERRQ(ierr)


end subroutine readSSDFromFile

end module Init_module
